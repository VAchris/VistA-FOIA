SDMAPI2 ;MAKE APPOINTMENT API; 05/28/2012  11:46 AM
 ;;;Scheduling;;05/28/2012;
FRSTAVBL(RETURN,SC) ; Get first available date
 D FRSTAVBL^SDMDAL2(.RETURN,SC)
 Q 1
 ;
CHKAPP(RETURN,SC,DFN,SD,FLDS) ; Check make appointment
 S RETURN=0
 D CHKPAT(.RETURN,DFN,SD)
 D CHKCLN(.RETURN,SC,SD)
 Q RETURN
 ;
CHKPAT(RETURN,DFN,SD) ; 
 N PAT
 S RETURN=0
 D GETPAT^SDMAPI3(.PAT,DFN)
 I +$G(PAT("DATE OF DEATH"))>0 D ERRX^SDAPIE(.RETURN,"PATDIED") Q RETURN
 I $P(SD,".",1)<$P(PAT("DATE OF BIRTH"),U,1) D ERRX^SDAPIE(.RETURN,"APTPPAB") Q RETURN
 S RETURN=1
 Q RETURN
 ;
CHKCLN(RETURN,SC,SD) ;
 N CLN,VAL,PATT,HOL,TXT,X1,X2
 S RETURN=0
 D GETCLN^SDMDAL1(.CLN,SC)
 S VAL=$$CLNCK^SDMAPI1(.RETURN,SC) Q:VAL=0 VAL
 S VAL=$$CLNRGHT^SDMAPI1(.RETURN,SC) Q:VAL=0 VAL
 S FRSTA=$$GETFSTA^SDMDAL1(SC) I FRSTA,$P(SD,".",1)<FRSTA D ERRX^SDAPIE(.RETURN,"APTPCLA") Q 0
 D GETPATT^SDMDAL1(.PATT,SC,SD) I $G(PATT(0))'["[" D ERRX^SDAPIE(.RETURN,"APTCLUV") Q 0
 S X1=DT,SDEDT=$G(CLN(2002))
 S:SDEDT'>0 SDEDT=365
 S X2=SDEDT D C^%DTC S SDEDT=X
 I $P(SD,".")'<SDEDT D ERRX^SDAPIE(.RETURN,"APTEXCD") Q 0
 D GETHOL^SDMDAL1(.HOL,$P(SD,"."))
 S SDSOH=$S('$D(CLN(1918.5)):0,CLN(1918.5)']"":0,1:1)
 I $D(HOL(0)),'SDSOH S TXT(1)=$P(HOL(0),U,2) D ERRX^SDAPIE(.RETURN,"APTSHOL",.TXT) Q 0
 S RETURN=1
 Q RETURN
 ;
MAKE(RETURN,DFN,SC,SD,TYPE,LEN,INFO,STATUS) ; Make appointment
 D CHKAPP(.RETURN,SC,DFN,SD,LEN) Q:'$G(RETURN) 0
 N CLN,S,SM,SDY
 D LOCKST^SDMDAL1(SC,SD)
 D LOCKS^SDMDAL1(SC,SD)
 D GETCLN^SDMDAL1(.CLN,SC,1)
 S SM=$$DECAVA(.CLN,SC,SD,LEN,.S)
 D SETST^SDMDAL1(SC,SD,S)
 S SDY=$$MAKE^SDMDAL1(SC,SD,DFN,LEN,SM,DUZ)
 D UNLCKS^SDMDAL1(SC,SD)
 D UNLCKST^SDMDAL1(SC,SD)
 D MAKE^SDMDAL3(DFN,SD,SC,TYPE)
 D MAKE^SDAMEVT(DFN,SD,SC,SDY,0)
 Q 1
 ;
DECAVA(CLN,SC,SD,LEN,PATT) ; Decrease availability
 N AV,S,STARTDAY,HSI,SI,SDDIF,SM
 S SM=0
 D GETPATT^SDMDAL1(.AV,SC,SD)
 S S=$G(AV(0)),SB=CLN(1914)
 S STARTDAY=$S($L(SB):SB,1:8),SB=STARTDAY-1/100
 S X=CLN(1917),HSI=$S(X=1:X,X:X,1:4),SI=$S(X="":4,X<3:4,X:X,1:4)
 S STR="#@!$* XXWVUTSRQPONMLKJIHGFEDCBA0123456789jklmnopqrstuvwxyz",SDDIF=$S(HSI<3:8/HSI,1:2)
 S I=SD#1-SB*100,ST=I#1*SI\.6+($P(I,".")*SI),SS=LEN*HSI/60*SDDIF+ST+ST
 I SM<7 S %=$F(S,"[",SS-1) S:'%!(CLN(1917)<3) %=999 I $F(S,"]",SS)'<%!(SDDIF=2&$E(S,ST+ST+1,SS-1)["[") S SM=7
 I ST+ST>$L(S),$L(S)<80 S S=S_" "
 S SDNOT=1   ;SD*5.3*490 naked Do added below
 F I=ST+ST:SDDIF:SS-SDDIF S ST=$E(S,I+1) S:ST="" ST=" " S Y=$E(STR,$F(STR,ST)-2) S:Y'?1NL&(SM<6) SM=6 S ST=$E(S,I+2,999) D  S:ST="" ST=" " S S=$E(S,1,I)_Y_ST
 .Q:ST'=""
 .Q:+LEN'>CLN(1912)
 .S ST="   "
 .Q
 S PATT=S
 Q SM
 ;
SLOTS(RETURN,SC) ; Get available slots
 D SLOTS^SDMDAL2(.RETURN,SC)
 Q
 ;
SCEXST(RETURN,CSC) ; Get Stop Cod Exception status
 N RET
 D SCEXST^SDMDAL2(.RET,CSC)
 S RETURN=RET
 I RET>0 S LAST=99999999999,LAST=$O(RET("EFFECTIVE DATE",LAST),-1) D
 . M RETURN=RET("EFFECTIVE DATE",LAST)
 Q RETURN
 ;
LSTAPPT(RETURN,SEARCH,START,NUMBER) ; Lists appointment types
 N RET,DL,IN
 S:'$D(START) START="" S:'$D(SEARCH) SEARCH=""
 S:'$G(NUMBER) NUMBER=""
 S RETURN=0
 D LSTAPPT^SDMDAL2(.RET,SEARCH,.START,NUMBER)
 S RETURN(0)=RET("DILIST",0)
 S DL="DILIST"
 F IN=1:1:$P(RETURN(0),U,1) D
 . S RETURN(IN)=""
 . S RETURN(IN,"ID")=RET(DL,2,IN)
 . S RETURN(IN,"NAME")=RET(DL,"ID",IN,".01")
 S RETURN=1
 Q 1
 ;
GETAPPT(RETURN,TYPE) ; Returns Appointment Type detail
 D GETAPPT^SDMDAL2(.RETURN,TYPE,1,1,1)
 Q 1
 ;
GETELIG(RETURN,ELIG) ; Returns Eligibility Code detail 
 D GETELIG^SDMDAL2(.RETURN,ELIG,1,1,1)
 Q 1
 ;
HASPEND(RETURN,PAT,DT) ; Check if patient has panding appointments
 D HASPEND^SDMDAL2(.RETURN,PAT,DT)
 Q 1
 ;
GETPEND(RETURN,PAT,DT) ; Get pending appointments
 N CNT,SCAP,APP,CLN
 S CNT=""
 D GETPEND^SDMDAL2(.APP,PAT,DT)
 F  S CNT=$O(APP(CNT)) Q:CNT=""  D
 . S RETURN(CNT,"COLLATERAL VISIT")=APP(CNT,13)
 . S RETURN(CNT,"APPOINTMENT TYPE")=$$APTYNAME^SDMDAL2(APP(CNT,9.5))
 . S RETURN(CNT,"LAB DATE/TIME")=APP(CNT,2)
 . S RETURN(CNT,"X-RAY DATE/TIME")=APP(CNT,3)
 . S RETURN(CNT,"EKG DATE/TIME")=APP(CNT,4)
 . D GETCLN^SDMAPI1(.CLN,APP(CNT,.01))
 . S RETURN(CNT,"CLINIC")=$P(CLN("NAME"),U,2)
 . D GETSCAP^SDMDAL1(.SCAP,APP(CNT,.01),PAT,CNT)
 . S RETURN(CNT,"LENGTH OF APP'T")=$G(SCAP(CNT,1))
 . S RETURN(CNT,"CONSULT LINK")=$G(SCAP(CNT,688))
 Q
 ;
GETAPTS(RETURN,DFN,SD) ; Get patient appointments
 D GETAPTS^SDMDAL2(.RETURN,DFN,.SD)
 Q 1
 ;
LSTCRSNS(RETURN,SEARCH,START,NUMBER) ; Return cancelation reasons.
 N LST
 M LST=RETURN
 D LSTCRSNS^SDMDAL2(.LST,$G(SEARCH),.START,$G(NUMBER))
 D BLDLST^SDMAPI(.RETURN,.LST)
 Q RETURN
 ;
CHKCAN(RETURN,DFN,SC,SD) ; Verify cancel
 N APT
 S RETURN=0
 D GETAPTS^SDMDAL2(.APT,DFN,.SD)
 I APT("APT",SD,"STATUS")["C" D  Q RETURN
 . D ERRX^SDAPIE(.RETURN,"APTCAND")
 I $$CODT^SDCOU(DFN,SD,SC) D  Q RETURN
 . D ERRX^SDAPIE(.RETURN,"APTCCHO")
 D CLNRGHT^SDMAPI1(.RET,+SC)
 I RET=0 D  Q RETURN
 . S TXT(1)=RET("CLN"),TXT(2)=$C(10) 
 . D ERRX^SDAPIE(.RETURN,"APTCRGT",.TXT)
 I '$$CHKSPC(.STAT,DFN,SD) D  Q RETURN
 . D ERRX^SDAPIE(.RETURN,"APTCNPE",.TXT)
 S RETURN=1
 Q RETURN
 ;
CHKSPC(RETURN,DFN,SD) ; Check if status permit cancelation
 N APT0,STATUS,IND,STAT,STATS
 S RETURN=0
 S APT0=$$GETAPT0^SDMDAL2(DFN,SD)
 S STATUS=+$$STATUS^SDAM1(DFN,SD,+$G(APT0),$G(APT0))
 D LSTCSTA1^SDMDAL2(.STAT)
 D BLDLST^SDMAPI(.STATS,.STAT)
 S IND=0
 F  S IND=$O(STATS(IND)) Q:IND=""!(RETURN=1)  D
 . I STATS(IND,"ID")=STATUS S RETURN=1 Q
 Q RETURN
 ;
CANCEL(RETURN,DFN,SC,SD,TYP,RSN,RMK) ; Cancel appointment
 S RETURN=0
 D CHKCAN(.RETURN,DFN,SC,SD) Q:RETURN=0
 S CDATE=$$NOW^XLFDT
 D GETSCAP^SDMDAL1(.CAPT,SC,DFN,SD)
 S CIFN=CAPT("IFN")
 S OUSR=CAPT("USER"),ODT=CAPT("DATE")
 N SDATA,SDCPHDL
 S SDCPHDL=$$HANDLE^SDAMEVT(1)
 D BEFORE^SDAMEVT(.SDATA,DFN,SD,SC,CIFN,SDCPHDL)
 D CANCEL^SDMDAL3(.ERR,DFN,SD,TYP,RSN,RMK,DUZ,OUSR,ODT)
 S OIFN=$$COVERB^SDMDAL1(SC,SD,CIFN)
 D CANCEL^SDMDAL1(SC,SD,DFN,CIFN)
 D CANCEL^SDAMEVT(.SDATA,DFN,SD,SC,CIFN,0,SDCPHDL)
 S RETURN=1
 Q RETURN
 ;
CHECKIN(RETURN,DFN,SD,SC) ; Check in appointment
 N CAPT
 D GETSCAP^SDMDAL1(.CAPT,SC,DFN,SD)
 S CIFN=CAPT("IFN")
 I 'CIFN D ERRX^SDAPIE(.RETURN,"APTCIPE") Q 0
 N SDATA,SDCIHDL,X 
 S SDATA=CIFN_U_DFN_U_SD_U_SC,SDCIHDL=$$HANDLE^SDAMEVT(1)
 D BEFORE^SDAMEVT(.SDATA,DFN,SD,SC,CIFN,SDCIHDL)
 S %=$$CHKCIN(.RETURN,DFN,SD,+SDATA("BEFORE","STATUS")) Q:'% 0
 D CHECKIN^SDMDAL1(CIFN,SC,SD,DUZ,$$NOW^XLFDT())
 D AFTER^SDAMEVT(.SDATA,DFN,SD,SC,CIFN,SDCIHDL)
 M RETURN=SDATA
 I SDATA("BEFORE","STATUS")'=SDATA("AFTER","STATUS") D
 . D EVT^SDAMEVT(.SDATA,4,0,SDCIHDL) ; 4 := ci evt , 0 := interactive mode
 Q 1
 ;
CHKCIN(RETURN,DFN,SD,STATUS) ; Check in check
 N DT
 S RETURN=0
 S %=$$CHKSPCI(.RETURN,DFN,SD,STATUS) Q:'%
 S DT=$$NOW^XLFDT
 I $P(SD,".")>DT D ERRX^SDAPIE(.RETURN,"APTCITS") Q 0
 Q 1
 ;
CHKSPCI(RETURN,DFN,SD,STATUS) ; Check if status permit check in
 N IND,STAT,STATS
 S RETURN=0
 D LSTCIST1^SDMDAL2(.STAT)
 D BLDLST^SDMAPI(.STATS,.STAT)
 S IND=0
 F  S IND=$O(STATS(IND)) Q:IND=""!(RETURN=1)  D
 . I STATS(IND,"ID")=STATUS S RETURN=1 Q
 I 'RETURN D ERRX^SDAPIE(.RETURN,"APTCIPE")
 Q RETURN
 ;
