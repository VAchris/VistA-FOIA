SDMAPI2 ;MAKE APPOINTMENT API; 05/28/2012  11:46 AM
 ;;;Scheduling;;05/28/2012;
FRSTAVBL(RETURN,SC) ; Get first available date
 D FRSTAVBL^SDMDAL2(.RETURN,SC)
 Q 1
 ;
CHKAPP(RETURN,SC,DFN,SD,LEN,LVL) ; Check make appointment
 N PAT
 N CLN,VAL,PATT,HOL,TXT,X1,X2,APT,CAPT
 S RETURN=1
 S:'$G(LVL) LVL=7
 D GETPAT^SDMAPI3(.PAT,DFN) ; get patient data
 D GETCLN^SDMDAL1(.CLN,SC,1) ; get clinic data
 ;check if patient is dead
 I +$G(PAT("DATE OF DEATH"))>0 D ERRX^SDAPIE(.RETURN,"PATDIED") Q RETURN
 ;check if clinic is valid (stop code)
 S VAL=$$CLNCK^SDMAPI1(.RETURN,SC) Q:VAL=0 VAL
 ;check user permissions
 S VAL=$$CLNRGHT^SDMAPI1(.RETURN,SC) Q:VAL=0 VAL
 ;verify that day hasn't been canceled via "SET UP A CLINIC"
 D GETPATT^SDMDAL1(.PATT,SC,SD) I $G(PATT(0))'["[" S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTCLUV") Q 0
 ;check if schedule on holiday is permited
 D GETHOL^SDMDAL1(.HOL,$P(SD,"."))
 S SDSOH=$S('$D(CLN(1918.5)):0,CLN(1918.5)']"":0,1:1)
 I $D(HOL(0)),'SDSOH S TXT(1)=$P(HOL(0),U,2) S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTSHOL",.TXT) Q 0
 ;check inactive clinic period
 I CLN(2505),$P(SD,".")'<CLN(2505),$S('CLN(2506):1,CLN(2506)>$P(SD,".")!('CLN(2506)):1,1:0) D  Q 0
 . S TXT(1)=$$DTS^SDMAPI(CLN(2505))
 . S:CLN(2506) TXT(2)=" and reactivated on "_$$DTS^SDMAPI(CLN(2506))
 . S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTCINV",.TXT) 
 ;check if exceed max days for future appointment
 S X1=DT,SDEDT=$G(CLN(2002))
 S:SDEDT'>0 SDEDT=365
 S X2=SDEDT D C^%DTC S SDEDT=X
 I $P(SD,".")'<SDEDT S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTEXCD") Q 0
 ;check if patient has an active appointment on the same time
 D GETAPTS^SDMDAL2(.APT,DFN,SD)
 I $D(APT),APT("APT",SD,"STATUS")'["C"  D  Q
 . D GETSCAP^SDMDAL1(.CAPT,SC,DFN,SD) Q:'$D(CAPT)
 . S TXT(1)="("_CAPT("LENGTH")_" MINUTES)"
 . S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTPAHA",.TXT,2)
 Q:RETURN=0 0
 ;check if patient has an active appointment on the same day
 I LVL>2 D
 . K APT N IDX S IDX=""
 . D GETDAPTS^SDMDAL2(.APT,DFN,$P(SD,"."))
 . F  S IDX=$O(APT(IDX)) Q:IDX=""  I APT(IDX,2)'["C"  D  Q
 . . K TXT S TXT(1)="(AT "_$E(IDX_0,9,10)_":"_$E(IDX_"000",11,12)_")"
 . . S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTPHSD",.TXT,3)
 Q:RETURN=0 0
 ;check if patient has an canceled appointment on the same time
 I LVL'<2 D
 . K APT
 . D GETAPTS^SDMDAL2(.APT,DFN,SD)
 . I $D(APT),APT("APT",SD,"STATUS")["P" D 
 . . S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTPPCP",,2)
 Q:RETURN=0 0
 ;check if date is prior to patient birth date
 I $P(SD,".",1)<$P(PAT("DATE OF BIRTH"),U,1) S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTPPAB") Q RETURN
 ;check if date is prior to clinic availability
 S FRSTA=$$GETFSTA^SDMDAL1(SC) I FRSTA,$P(SD,".",1)<FRSTA S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTPCLA") Q 0
 ;check overbook
 S %=$$CHKOVB(.RETURN,.CLN,SC,SD,LEN,LVL) Q:RETURN=0 RETURN
 S RETURN=1
 Q RETURN
 ;
CHKOVB(RETURN,CLN,SC,SD,LEN,LVL)
 N TXT,ACC,SM
 S RETURN=1 S:'$G(LVL) LVL=7
 S SM=$$DECAVA(.CLN,SC,SD,LEN,.PP)
 Q:'SM 0
 D GETXUS^SDMDAL3(.ACC,DUZ)
 I '$D(ACC("SDOB")) S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTNOST") Q 0
 S MAXOB=CLN(1918)
 S OBNO=$$GETOBNO(SC,SD)
 S TXT(1)=MAXOB,TXT(2)=$S(OBNO>1:"S",1:"") 
 I OBNO>MAXOB,'$D(ACC("SDMOB")) S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTOAPD",.TXT) Q 0
 I OBNO>MAXOB,LVL>1 S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTEXOB",,2) Q 0
 I SM=6,LVL>1 S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTOVBK",,2) Q 0
 I SM=7 S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTOVOS",,2) Q 0
 I SM=1 S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTCBCP") Q 0
 Q RETURN
 ;
GETOBNO(SC,SD) ;
 N IND,CNT
 S IND="",CNT=0
 D GETDAYA^SDMDAL1(.APTS,SC,SD)
 F  S IND=$O(APTS(IND)) Q:IND=""  S:APTS(IND,"OB")>0 CNT=CNT+1
 Q CNT
 ;
MAKE(RETURN,DFN,SC,SD,TYPE,LEN,LVL,INFO) ; Make appointment
 S:'$G(LVL) LVL=7
 S RETURN=0
 D CHKAPP(.RETURN,SC,DFN,SD,LEN,LVL)
 I RETURN=0,$P(RETURN(0),U,3)'>LVL Q RETURN
 I RETURN=0,$P(RETURN(0),U)="APTPAHA" D
 . D CANCEL(.RETURN,DFN,SC,SD,"C",13,"")
 E  Q:'$G(RETURN)&('$G(LVL)) 0
 N CLN,S,SM,SDY
 S %=$$LOCKST^SDMDAL1(SC,SD) I '% S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTLOCK") Q 0
 S %=$$LOCKS^SDMDAL1(SC,SD) I '% S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTLOCK") Q 0
 D GETCLN^SDMDAL1(.CLN,SC,1)
 S SM=$$DECAVA(.CLN,SC,SD,+LEN,.S)
 D SETST^SDMDAL1(SC,SD,S)
 D MAKE^SDMDAL1(SC,SD,DFN,+LEN,SM,DUZ)
 D UNLCKS^SDMDAL1(SC,SD)
 D UNLCKST^SDMDAL1(SC,SD)
 S STAT=$$INP^SDAM2(DFN,SD)
 D MAKE^SDMDAL3(DFN,SD,SC,TYPE,STAT,$G(INFO))
 D GETSCAP^SDMDAL1(.SCAP,SC,DFN,SD)
 S SDY=SCAP("IFN")
 D MAKE^SDAMEVT(DFN,SD,SC,SDY,0)
 Q 1
 ;
DECAVA(CLN,SC,SD,LEN,PATT) ; Decrease availability
 N AV,S,SB,X,Y,I,SS,ST,STR,STARTDAY,HSI,SI,SDDIF,SM,CAN
 S SM=0,CAN=0
 D GETPATT^SDMDAL1(.AV,SC,SD)
 S S=$G(AV(0)),SB=CLN(1914)
 S STARTDAY=$S($L(SB):SB,1:8),SB=STARTDAY-1/100
 S X=CLN(1917),HSI=$S(X=1:X,X:X,1:4),SI=$S(X="":4,X<3:4,X:X,1:4)
 S STR="#@!$* XXWVUTSRQPONMLKJIHGFEDCBA0123456789jklmnopqrstuvwxyz",SDDIF=$S(HSI<3:8/HSI,1:2)
 S I=SD#1-SB*100,ST=I#1*SI\.6+($P(I,".")*SI),SS=LEN*HSI/60*SDDIF+ST+ST
 I SM<7 S %=$F(S,"[",SS-1) S:'%!(CLN(1917)<3) %=999 I $F(S,"]",SS)'<%!(SDDIF=2&$E(S,ST+ST+1,SS-1)["[") S SM=7
 I ST+ST>$L(S),$L(S)<80 S S=S_" "
 S SDNOT=1   ;SD*5.3*490 naked Do added below
 F I=ST+ST:SDDIF:SS-SDDIF S ST=$E(S,I+1) S:ST="" ST=" " S Y=$E(STR,$F(STR,ST)-2) S:S["CAN"!(ST="X"&($D(AV(1)))) CAN=1 Q:CAN  S:Y'?1NL&(SM<6) SM=6 S ST=$E(S,I+2,999) D  S:ST="" ST=" " S S=$E(S,1,I)_Y_ST
 .Q:ST'=""
 .Q:+LEN'>CLN(1912)
 .S ST="   "
 .Q
 S PATT=S
 Q:CAN CAN
 Q SM
 ;
SLOTS(RETURN,SC) ; Get available slots
 D SLOTS^SDMDAL2(.RETURN,SC)
 Q
 ;
SCEXST(RETURN,CSC) ; Get Stop Cod Exception status
 N RET
 D SCEXST^SDMDAL2(.RET,CSC)
 S RETURN=RET
 I RET>0 S LAST=99999999999,LAST=$O(RET("EFFECTIVE DATE",LAST),-1) D
 . M RETURN=RET("EFFECTIVE DATE",LAST)
 Q RETURN
 ;
LSTAPPT(RETURN,SEARCH,START,NUMBER) ; Lists appointment types
 N RET,DL,IN
 S:'$D(START) START="" S:'$D(SEARCH) SEARCH=""
 S:'$G(NUMBER) NUMBER=""
 S RETURN=0
 D LSTAPPT^SDMDAL2(.RET,$$UP^XLFSTR(SEARCH),.START,NUMBER)
 S RETURN(0)=RET("DILIST",0)
 S DL="DILIST"
 F IN=1:1:$P(RETURN(0),U,1) D
 . S RETURN(IN)=""
 . S RETURN(IN,"ID")=RET(DL,2,IN)
 . S RETURN(IN,"NAME")=RET(DL,"ID",IN,".01")
 S RETURN=1
 Q 1
 ;
GETAPPT(RETURN,TYPE) ; Returns Appointment Type detail
 D GETAPPT^SDMDAL2(.RETURN,TYPE,1,1,1)
 Q 1
 ;
GETELIG(RETURN,ELIG) ; Returns Eligibility Code detail 
 D GETELIG^SDMDAL2(.RETURN,ELIG,1,1,1)
 Q 1
 ;
HASPEND(RETURN,PAT,DT) ; Check if patient has panding appointments
 D HASPEND^SDMDAL2(.RETURN,PAT,DT)
 Q 1
 ;
GETPEND(RETURN,PAT,DT) ; Get pending appointments
 N CNT,SCAP,APP,CLN
 S CNT=""
 D GETPEND^SDMDAL2(.APP,PAT,DT)
 F  S CNT=$O(APP(CNT)) Q:CNT=""  D
 . S RETURN(CNT,"COLLATERAL VISIT")=APP(CNT,13)
 . S RETURN(CNT,"APPOINTMENT TYPE")=$$APTYNAME^SDMDAL2(APP(CNT,9.5))
 . S RETURN(CNT,"LAB DATE/TIME")=APP(CNT,2)
 . S RETURN(CNT,"X-RAY DATE/TIME")=APP(CNT,3)
 . S RETURN(CNT,"EKG DATE/TIME")=APP(CNT,4)
 . D GETCLN^SDMAPI1(.CLN,APP(CNT,.01))
 . S RETURN(CNT,"CLINIC")=$P(CLN("NAME"),U,2)
 . D GETSCAP^SDMDAL1(.SCAP,APP(CNT,.01),PAT,CNT)
 . S RETURN(CNT,"LENGTH OF APP'T")=$G(SCAP(CNT,1))
 . S RETURN(CNT,"CONSULT LINK")=$G(SCAP(CNT,688))
 Q
 ;
GETAPTS(RETURN,DFN,SD) ; Get patient appointments
 D GETAPTS^SDMDAL2(.RETURN,DFN,.SD)
 Q 1
 ;
LSTCRSNS(RETURN,SEARCH,START,NUMBER) ; Return cancelation reasons.
 N LST
 M LST=RETURN
 D LSTCRSNS^SDMDAL2(.LST,$$UP^XLFSTR($G(SEARCH)),.START,$G(NUMBER))
 D BLDLST^SDMAPI(.RETURN,.LST)
 Q RETURN
 ;
CHKCAN(RETURN,DFN,SC,SD) ; Verify cancel
 N APT
 S RETURN=0
 D GETAPTS^SDMDAL2(.APT,DFN,.SD)
 I APT("APT",SD,"STATUS")["C" D  Q RETURN
 . D ERRX^SDAPIE(.RETURN,"APTCAND")
 I $$CODT^SDCOU(DFN,SD,SC) D  Q RETURN
 . D ERRX^SDAPIE(.RETURN,"APTCCHO")
 D CLNRGHT^SDMAPI1(.RET,+SC)
 I RET=0 D  Q RETURN
 . S TXT(1)=RET("CLN"),TXT(2)=$C(10) 
 . D ERRX^SDAPIE(.RETURN,"APTCRGT",.TXT)
 I '$$CHKSPC(.STAT,DFN,SD) D  Q RETURN
 . D ERRX^SDAPIE(.RETURN,"APTCNPE",.TXT)
 S RETURN=1
 Q RETURN
 ;
CHKSPC(RETURN,DFN,SD) ; Check if status permit cancelation
 N APT0,STATUS,IND,STAT,STATS
 S RETURN=0
 S APT0=$$GETAPT0^SDMDAL2(DFN,SD)
 S STATUS=+$$STATUS^SDAM1(DFN,SD,+$G(APT0),$G(APT0))
 D LSTCSTA1^SDMDAL2(.STAT)
 D BLDLST^SDMAPI(.STATS,.STAT)
 S IND=0
 F  S IND=$O(STATS(IND)) Q:IND=""!(RETURN=1)  D
 . I STATS(IND,"ID")=STATUS S RETURN=1 Q
 Q RETURN
 ;
CANCEL(RETURN,DFN,SC,SD,TYP,RSN,RMK) ; Cancel appointment
 S RETURN=0
 D CHKCAN(.RETURN,DFN,SC,SD) Q:RETURN=0
 S CDATE=$$NOW^XLFDT
 D GETSCAP^SDMDAL1(.CAPT,SC,DFN,SD)
 S CIFN=CAPT("IFN")
 S OUSR=CAPT("USER"),ODT=CAPT("DATE")
 N SDATA,SDCPHDL
 S SDCPHDL=$$HANDLE^SDAMEVT(1)
 D BEFORE^SDAMEVT(.SDATA,DFN,SD,SC,CIFN,SDCPHDL)
 S CDT=$$NOW^XLFDT()
 D CANCEL^SDMDAL3(.ERR,DFN,SD,TYP,RSN,RMK,$E(CDT,1,12),DUZ,OUSR,ODT)
 S OIFN=$$COVERB^SDMDAL1(SC,SD,CIFN)
 D CANCEL^SDMDAL1(SC,SD,DFN,CIFN)
 D CANCEL^SDAMEVT(.SDATA,DFN,SD,SC,CIFN,0,SDCPHDL)
 S RETURN=1
 Q RETURN
 ;
CHECKIN(RETURN,DFN,SD,SC) ; Check in appointment
 N CAPT
 D GETSCAP^SDMDAL1(.CAPT,SC,DFN,SD)
 S CIFN=CAPT("IFN")
 I 'CIFN D ERRX^SDAPIE(.RETURN,"APTCIPE") Q 0
 N SDATA,SDCIHDL,X 
 S SDATA=CIFN_U_DFN_U_SD_U_SC,SDCIHDL=$$HANDLE^SDAMEVT(1)
 D BEFORE^SDAMEVT(.SDATA,DFN,SD,SC,CIFN,SDCIHDL)
 S %=$$CHKCIN(.RETURN,DFN,SD,+SDATA("BEFORE","STATUS")) Q:'% 0
 D CHECKIN^SDMDAL1(CIFN,SC,SD,DUZ,$$NOW^XLFDT())
 D AFTER^SDAMEVT(.SDATA,DFN,SD,SC,CIFN,SDCIHDL)
 M RETURN=SDATA
 I SDATA("BEFORE","STATUS")'=SDATA("AFTER","STATUS") D
 . D EVT^SDAMEVT(.SDATA,4,0,SDCIHDL) ; 4 := ci evt , 0 := interactive mode
 Q 1
 ;
CHKCIN(RETURN,DFN,SD,STATUS) ; Check in check
 N DT
 S RETURN=0
 S %=$$CHKSPCI(.RETURN,DFN,SD,STATUS) Q:'%
 S DT=$$NOW^XLFDT
 I $P(SD,".")>DT D ERRX^SDAPIE(.RETURN,"APTCITS") Q 0
 Q 1
 ;
CHKSPCI(RETURN,DFN,SD,STATUS) ; Check if status permit check in
 N IND,STAT,STATS
 S RETURN=0
 D LSTCIST1^SDMDAL2(.STAT)
 D BLDLST^SDMAPI(.STATS,.STAT)
 S IND=0
 F  S IND=$O(STATS(IND)) Q:IND=""!(RETURN=1)  D
 . I STATS(IND,"ID")=STATUS S RETURN=1 Q
 I 'RETURN D ERRX^SDAPIE(.RETURN,"APTCIPE")
 Q RETURN
 ;
NOSHOW(RETURN,DFN,SC,SD,LVL) ; No-show appointment
 N APT0,STATUS,APTSTAT
 S:'$D(LVL) LVL=7
 S APT0=$$GETAPT0^SDMDAL2(DFN,SD)
 S APTSTAT=$P(APT0,U,2)
 S STATUS=$$STATUS^SDAM1(DFN,SD,+$G(APT0),$G(APT0))
 S RETURN=0
 S %=$$CHKNS(.RETURN,APT0,+STATUS,LVL)
 I RETURN=0,$P(RETURN(0),U,3)'>LVL Q RETURN
 N IENS,FDA,CIFN,CAPT
 S IENS=SD_","_DFN_","
 D GETSCAP^SDMDAL1(.CAPT,SC,DFN,SD)
 S CIFN=CAPT("IFN")
 S CNSTLNK=$G(CAPT("CONSULT"))
 S RETURN("BEFORE")=STATUS
 N SDNSHDL S SDNSHDL=$$HANDLE^SDAMEVT(1)
 D BEFORE^SDAMEVT(.SDATA,DFN,SD,SC,CIFN,SDNSHDL)
 N MSG
 I APTSTAT=""!(APTSTAT="NT") D
 . S FDA(2.98,IENS,3)="N"
 . S FDA(2.98,IENS,14)=DUZ
 . S FDA(2.98,IENS,15)=$$NOW^XLFDT()
 E  D
 . S FDA(2.98,IENS,3)="@"
 . S FDA(2.98,IENS,14)="@"
 . S FDA(2.98,IENS,15)="@"
 D FILE^DIE("","FDA","MSG")
 D NOSHOW^SDAMEVT(.SDATA,DFN,SD,SC,CIFN,0,SDNSHDL)
 D:+$G(CNSTLNK) NOSHOW^SDCNSLT(SC,SD,DFN,CNSTLNK,CIFN,.AUTO,.NSDIE,.NSDA)
 S APT0=$$GETAPT0^SDMDAL2(DFN,SD)
 S APTSTAT=$P(APT0,U,2)
 S STATUS=$$STATUS^SDAM1(DFN,SD,+$G(APT0),$G(APT0))
 S RETURN("AFTER")=STATUS
 Q 1
 ;
CHKNS(RETURN,APT0,STATUS,LVL) ; Check no-show
 N STAT
 S RETURN=1
 S %=$$CHKSPNS(.RETURN,+STATUS) Q:'% 0
 S STAT=$P(APT0,U,2)
 I STAT="I" S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTNSIA") Q RETURN
 I LVL>1,STAT["A" S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTNSAR",,2) Q RETURN
 I LVL>1,STAT]"",STAT'["A" S RETURN=0 D ERRX^SDAPIE(.RETURN,"APTNSAL",,2) Q RETURN
 Q RETURN
 ;
CHKSPNS(RETURN,STATUS) ; Check if status of appt permits no-show
 N IND,STAT,STATS
 S RETURN=0
 D LSTNSTA1^SDMDAL2(.STAT)
 D BLDLST^SDMAPI(.STATS,.STAT)
 S IND=0
 F  S IND=$O(STATS(IND)) Q:IND=""!(RETURN=1)  D
 . I STATS(IND,"ID")=STATUS S RETURN=1 Q
 I 'RETURN D ERRX^SDAPIE(.RETURN,"APTNSCE")
 Q RETURN
 ;
