SDMDAL3 ;MAKE APPOINTMENT DAL; 05/28/2012  11:46 AM
 ;;;Scheduling;;05/28/2012;
GETPATS(RETURN,SEARCH,START,NUMBER) ; Get patients
 N FILE,FIELDS,RET,SCR,INDX
 S FILE="2",FIELDS="@;.01;.03;.09;391;1901",INDX="B"
 S:$D(START)=0 START="" S:$D(SEARCH)=0 SEARCH=""
 I $D(SEARCH),SEARCH?4N S INDX="BS"
 I $L(SEARCH)>1,SEARCH?.N S INDX="SSN"
 I $L(SEARCH)>0,SEARCH?1A4N S INDX="BS5"
 D LIST^DIC(FILE,"",FIELDS,"",$G(NUMBER),.START,SEARCH,INDX,.SCR,"","RETURN")
 Q
 ;
GETPAT(RETURN,PAT,INT,EXT,REZ) ; Get patient detail
 N FILE,SFILES,FLDS
 S FILE=2
 S FLDS("*")=""
 S SFILES(".3721")="",SFILES(".3721","N")="RATED DISABILITIES",SFILES(".3721","F")="2.04"
 S SFILES("2")="",SFILES("2","N")="RACE INFORMATION",SFILES("2","F")="2.02"
 S SFILES("6")="",SFILES("6","N")="ETHNICITY INFORMATION",SFILES("6","F")="2.06"
 D GETREC^SDMDAL(.RETURN,PAT,FILE,.FLDS,.SFILES,$G(INT),$G(EXT),$G(REZ))
 Q
 ;
LSTETNS(RETURN,SEARCH,START,NUMBER) ; Return ethnicity information.
 N FILE,FIELDS,RET,SCR
 S FILE="10.2",FIELDS="@;.01"
 S:$D(START)=0 START="" S:$D(SEARCH)=0 SEARCH=""
 S SCR="I $S('$D(^(.02)):1,$P(^(.02),U,1)=1:0,1:1)"
 D LIST^DIC(FILE,"",FIELDS,"",$G(NUMBER),.START,SEARCH,"B",.SCR,"","RETURN")
 Q
 ;
SETETN(PAT,ETN) ; Set patient ethnicity.
 N IENS,FDA,MSG
 S IENS=$O(^DPT(PAT,.06,0))_","_PAT_","
 S FDA(2.06,IENS,".01")=ETN
 S FDA(2.06,IENS,".02")=1
 D FILE^DIE("","FDA","MSG")
 Q
 ;
LSTRACES(RETURN,SEARCH,START,NUMBER) ; Return races.
 N FILE,FIELDS,RET,SCR
 S FILE="10",FIELDS="@;.01"
 S:$D(START)=0 START="" S:$D(SEARCH)=0 SEARCH=""
 S SCR="I $S('$D(^(.02)):1,$P(^(.02),U,1)=1:0,1:1)"
 D LIST^DIC(FILE,"",FIELDS,"",$G(NUMBER),.START,SEARCH,"B",.SCR,"","RETURN")
 Q
 ;
GETPRES(RETURN,PAT) ; Get patient races
 N FILE,SFILES,FLDS
 S FILE=2
 S SFILES("2")="",SFILES("2","N")="RACE INFORMATION",SFILES("2","F")="2.02"
 D GETREC^SDMDAL(.RETURN,PAT,FILE,.FLDS,.SFILES,$G(INT),$G(EXT),$G(REZ))
 Q
 ;
ADDRACE(PAT,RACE) ; Set patient race.
 N IENS,FDA,MSG
 S IENS="?+2,"_PAT_","
 S IENS(2)=RACE
 S FDA(2.02,IENS,".01")=RACE
 S FDA(2.02,IENS,".02")=1
 D UPDATE^DIE("","FDA","IENS","MSG")
 Q
 ;
GETAPPS(RETURN,DFN,SD) ; Get day appointment
 N S
 F S=$P(SD,"."):0 S S=+$O(^DPT(DFN,"S",S)) Q:$P(S,".")-($P(SD,"."))  D
 . S RETURN(SD)=+^(S,0)
 Q
 ;
MAKE(DFN,SD,SC,TYPE,STYP,STAT,RSN,USR,DT,SRT,NAAI) ; Make patient appointment
 N ERR,FDA,IENS
 I $D(^DPT(DFN,"S",SD,0)),$P(^(0),U,2)["C" D
 . S IENS=SD_","_DFN_","
 . S FDA(2.98,IENS,".01")=SC
 . S FDA(2.98,IENS,"3")="@"
 . S FDA(2.98,IENS,"9")=$G(RSN)
 . S FDA(2.98,IENS,"9.5")=TYPE
 . S FDA(2.98,IENS,"17")="@"
 . S FDA(2.98,IENS,"20")=DT
 . D FILE^DIE("","FDA","ERR")
 E  D
 . S IENS="?+2,"_DFN_","
 . S IENS(2)=SD
 . S FDA(2.98,IENS,.01)=SC
 . S FDA(2.98,IENS,"3")=STAT
 . S FDA(2.98,IENS,"9")=$G(RSN)
 . S FDA(2.98,IENS,"9.5")=TYPE
 . S FDA(2.98,IENS,"19")=USR
 . S FDA(2.98,IENS,"20")=DT
 . S FDA(2.98,IENS,"24")=$G(STYP)
 . S FDA(2.98,IENS,"25")=$G(SRT)
 . S FDA(2.98,IENS,"26")=$G(NAAI)
 . D UPDATE^DIE("","FDA","IENS","ERR")
 Q
 ;
CANCEL(RETURN,DFN,SD,TYP,RSN,RMK,CDT,USR,OUSR,ODT) ; Cancel appointment.
 N IENS,FDA
 S IENS=SD_","_DFN_","
 S FDA(2.98,IENS,3)=TYP
 S FDA(2.98,IENS,14)=USR
 S FDA(2.98,IENS,15)=CDT
 S FDA(2.98,IENS,16)=RSN
 S FDA(2.98,IENS,19)=OUSR
 S FDA(2.98,IENS,20)=ODT
 S:$G(RMK)]"" FDA(2.98,IENS,17)=$E(RMK,1,160)
 D FILE^DIE("","FDA","RETURN")
 Q
 ;
GETXUS(RETURN,USR) ; Get user access
 S:$D(^XUSEC("SDOB",USR)) RETURN("SDOB")=^XUSEC("SDOB",USR)
 S:$D(^XUSEC("SDMOB",USR)) RETURN("SDMOB")=^XUSEC("SDMOB",USR)
 Q
 ;
