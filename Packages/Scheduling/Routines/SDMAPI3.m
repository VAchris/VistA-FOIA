SDMAPI3 ;MAKE APPOINTMENT API; 05/28/2012  11:46 AM
 ;;;Scheduling;;05/28/2012;
LSTPATS(RETURN,SEARCH,START,NUMBER) ; Get patients by name
 N RET,DL,IN
 S:'$D(START) START="" S:'$D(SEARCH) SEARCH=""
 S:'$G(NUMBER) NUMBER=""
 S RETURN=0
 D GETPATS^SDMDAL3(.RET,SEARCH,.START,NUMBER)
 S RETURN(0)=RET("DILIST",0)
 S DL="DILIST"
 F IN=1:1:$P(RETURN(0),U,1) D
 . S RETURN(IN)=""
 . S RETURN(IN,"ID")=RET(DL,2,IN)
 . S RETURN(IN,"NAME")=RET(DL,"ID",IN,".01")
 . S RETURN(IN,"BIRTHDATE")=RET(DL,"ID",IN,".03")
 . S RETURN(IN,"SSN")=RET(DL,"ID",IN,".09")
 . S RETURN(IN,"TYPE")=RET(DL,"ID",IN,"391")
 . S RETURN(IN,"VETERAN")=RET(DL,"ID",IN,"1901")
 S RETURN=1
 Q 1
 ;
GETPAT(RETURN,PATIENT) ; Get a patient
 N TEXT
 S RETURN=0
 I '$G(PATIENT) S TEXT(1)="PATIENT" D ERRX^SDAPIE(.RETURN,"INVPARAM",.TEXT) Q 0
 D GETPAT^SDMDAL3(.RETURN,PATIENT,1,1,1)
 S RETURN=1
 Q RETURN
 ;
LSTETNS(RETURN,SEARCH,START,NUMBER) ; Get ethnicities
 N LST
 D LSTETNS^SDMDAL3(.LST,$G(SEARCH),.START,$G(NUMBER))
 D BLDLST^SDMAPI(.RETURN,.LST)
 Q RETURN
 ;
SETETN(PAT,ETN) ; Set patient ethnicity.
 D SETETN^SDMDAL3(PAT,ETN)
 Q 1
 ;
LSTRACES(RETURN,SEARCH,START,NUMBER) ; Get races
 N LST
 D LSTRACES^SDMDAL3(.LST,$G(SEARCH),.START,$G(NUMBER))
 D BLDLST^SDMAPI(.RETURN,.LST)
 Q RETURN
 ;
SETRACE(PAT,RACE) ; Set patient race.
 D ADDRACE^SDMDAL3(PAT,+RACE)
 Q 1
 ;
GETPRES(RETURN,PAT) ; Get patient races
 N LST,IND,CNT
 K RETURN
 D GETPRES^SDMDAL3(.LST,PAT)
 S IND=0,CNT=0
 S RETURN(0)=0
 F  S IND=$O(LST(2,IND)) Q:IND=""  D
 . S CNT=CNT+1
 . S RETURN(CNT)="",RETURN(CNT,"ID")=IND
 . S RETURN(CNT,"NAME")=LST(2,IND,.01)
 . S RETURN(0)=CNT_"^*^0^"
 Q 1
 ;
GETAPPS(RETURN,DFN,SD) ; Get patient appointments
 D GETAPPS^SDMDAL3(.RETURN,DFN,SD)
 Q
 ;
