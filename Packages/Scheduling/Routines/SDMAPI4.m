SDMAPI4 ;RGI/CBR - APPOINTMENT API; 08/10/2012
 ;;5.3;scheduling;**260003**;08/13/93;
CHECKO(RETURN,DFN,SD,SC) ; Check out
 N PAPT,CAPT,OE,APT0
 S PAPT(21)=""
 D GETSCAP^SDMDAL1(.CAPT,SC,DFN,SD)
 I '$D(CAPT) D ERRX^SDAPIE(.RETURN,"APTCOCN") Q 0
 S APT0=$$GETAPT0^SDMDAL2(DFN,SD)
 S STATUS=$$STATUS^SDAM1(DFN,SD,+$G(APT0),$G(APT0))
 S %=$$CHKCO(.RETURN,DFN,SD,+STATUS)
 Q:'RETURN 0
 I '$$NEW^SDPCE(SD) D ERRX^SDAPIE(.RETURN,"APTCONW",,2)
 S RETURN("SDOE")=$$GETAPT(DFN,SD,SC)
 S RETURN("COD")=CAPT("CHECKOUT")
 S OE(.04)="",OE(.05)=""
 D GETOE^SDMDAL4(.OE,RETURN("SDOE"))
 S RETURN("LOCATION")=OE(.04)
 S RETURN("VISIT")=OE(.05)
 S RETURN("COVISIT")=$P(APT0,U,11)
 I "^2^8^12^"[("^"_+STATUS_"^"),$P(STATUS,";",3)["CHECKED OUT" D
 . S RETURN("CO")=1
 . D ERRX^SDAPIE(.RETURN,"APTCOAC",,2)
 Q 1
 ;
GETAPT(DFN,SDT,SDCL,SDVIEN) ;Look-up Outpatient Encounter IEN for Appt
 ; Input  -- DFN      Patient file IEN
 ;           SDT      Appointment Date/Time
 ;           SDCL     Hospital Location file IEN for Appt
 ;           SDVIEN   Visit file pointer [optional]
 ; Output -- Outpatient Encounter file IEN
 N PAPT
 S PAPT(21)=""
 D GETPAPT^SDMDAL2(.PAPT,DFN,SD)
 I 'PAPT(21) D APPT^SDVSIT(DFN,SDT,SDCL,$G(SDVIEN)) D GETPAPT^SDMDAL2(.PAPT,DFN,SD)
 I PAPT(21) D VIEN^SDVSIT2(PAPT(21),$G(SDVIEN))
 Q +$G(PAPT(21))
 ;
CHKCO(RETURN,DFN,SD,STATUS) ; Check in check out
 S RETURN=0
 I '$D(STATUS) D
 . S APT0=$$GETAPT0^SDMDAL2(DFN,SD)
 . S STATUS=$$STATUS^SDAM1(DFN,SD,+$G(APT0),$G(APT0))
 S %=$$CHKSPCO(.RETURN,DFN,SD,+STATUS) Q:'% 0
 S DT=$$NOW^XLFDT
 I $P(SD,".")>DT D ERRX^SDAPIE(.RETURN,"APTCOTS") Q 0
 Q 1
 ;
CHKSPCO(RETURN,DFN,SD,STATUS) ; Check if status permit check in
 N IND,STAT,STATS
 S RETURN=0
 D LSTCOST1^SDMDAL2(.STAT)
 D BLDLST^SDMAPI(.STATS,.STAT)
 S IND=0
 F  S IND=$O(STATS(IND)) Q:IND=""!(RETURN=1)  D
 . I STATS(IND,"ID")=STATUS S RETURN=1 Q
 I 'RETURN D ERRX^SDAPIE(.RETURN,"APTCOCE")
 Q RETURN
 ;
CHKDCO(RETURN,PAPT,CAPT,OE,DFN,SD) ; Check delete check out
 S RETURN=0
 I 'PAPT(21)!('CAPT("CHECKOUT")) D ERRX^SDAPIE(.RETURN,"APTDCOD") Q 0
 I '$$NEW^SDPCE(OE(.01)) D ERRX^SDAPIE(.RETURN,"APTDCOO") Q 0
 S RETURN=1
 Q 1
 ;
DELCO(RETURN,DFN,SD) ; Delete check out
 N PAPT,CAPT,SDATA,SDELHDL,X
 S PAPT(21)="",PAPT(.01)=""
 S OE(.01)="",OE(.05)="",OE(.08)="",OE(.09)="",OE(.06)=""
 D GETPAPT^SDMDAL2(.PAPT,DFN,SD)
 D GETSCAP^SDMDAL1(.CAPT,PAPT(.01),DFN,SD)
 D GETOE^SDMDAL4(.OE,PAPT(21))
 S %=$$CHKDCO(.RETURN,.PAPT,.CAPT,.OE,DFN,SD)
 I RETURN=0 Q 0
 N SDOE,SDORG,SC,VSIT
 S SDOE=PAPT(21),SDORG=OE(.08),SC=PAPT(.01),VSIT=OE(.05)
 S SDELHDL=$$HANDLE^SDAMEVT(1)
 S X=$$DELVFILE^PXAPI("ALL",VSIT,"","","")
 D EVT^SDCOU1(SDOE,"BEFORE",.SDELHDL,.SDATA)
 I "^1^2^3^"[("^"_SDORG_"^") D DELCHLD(SDOE)
 N PDATA
 I SDORG=1 D
 . S PDATA(21)="@"
 . N CDATA S CDATA(303)="@"
 . D UPDCAPT^SDMDAL4(.CDATA,SC,SD,CAPT("IFN"))
 I SDORG=3 D
 . S PDATA(18)="@"
 D UPDPAPT^SDMDAL4(.PDATA,DFN,SD)
 D DELCLS^SDMDAL4(SDOE)
 D DELOE(SDOE)
 D EVT^SDCOU1(SDOE,"AFTER",SDELHDL,.SDATA)
 ; -- call pce to make sure its data is gone
 D DEAD^PXUTLSTP(VSIT)
 I 'OE(.06) D DELCLS^SDMDAL4(SDOE)
 S SDOE=$$GETAPT^SDVSIT2(DFN,SDT,SDCL)
 Q 1
 ;
DELOE(SDOE) ; Delete Outpatient Encounter
 N OE
 S OE(.05)="",OE(.01)=""
 D GETOE^SDMDAL4(.OE,SDOE)
 I '$$NEW^SDPCE(OE(.01)) Q
 D DELOE^SDMDAL4(SDOE)
 S X=$$KILL^VSITKIL(OE(.05))
 Q
 ;
DELCHLD(SDOEP) ;Delete Children
 N SDOEC,OE,CHLD
 S SDOEC=0
 D GETCHLD^SDMDAL4(.CHLD,SDOE)
 F  S SDOEC=$O(RETURN(SDOEC)) Q:'SDOEC  D
 . D DELOE(SDOEC)
 Q
 ;
