GMPLBLD3 ; SLC/MKB -- Bld PL Selection Lists cont ; 04/12/12
 ;;2.0;Problem List;**28,260002**;Aug 25, 1994
 ;
 ; This routine invokes IA #3991
 ;
ASSIGN ; Assign list to clinic, users: Expects GMPLSLST
 N DIE,DA,DR,RE,MSGT D FULL^VALM1 G:+$G(GMPLSLST)'>0 ASQ
 I '$$VALLIST^GMPLAPI6(.RET,+GMPLSLST) D  G ASQ
 . D BLD^DIALOG(1250000.102,,,"MSG")
 . D EN^DDIOL($C(7))
 . D EN^DDIOL(.MSG)
 . N DIR,DUOUT,DTOUT,DIRUT
 . S DIR(0)="E" D ^DIR
 . Q
 ;
 K MSG
 D BLD^DIALOG(1250000.103,,,"MSG")
 D EN^DDIOL(.MSG)
 D ASGCLIN(+GMPLSLST)
 D USERS("1") ; assign
ASQ S VALMBCK="R",VALMSG=$$MSG^GMPLX
 Q
 ;
ASGCLIN(GMPLSLST) ;
 N CLIN,RETURN,LST,SUF
 S SUF=""
 S %=$$GETLIST^GMPLAPI1(.LST,GMPLSLST,0,1)
 S:$D(LST("LST","CLINIC")) SUF=$G(LST("LST","CLINIC"))
 S:$L(SUF)>0 SUF=SUF_"// "
 S CLIN=$$CLINIC(,SUF)
 I $G(CLIN)>0 S %=$$ADDLOC^GMPLAPI5(.RETURN,GMPLSLST,CLIN)
 Q
 ;
CLINIC(PREFIX,SUFIX) ;
CL ;
 N PRMPT,LNAME,X,LSTS,Y,RETURN,LSTS,PREF,SUF
 S PREF=$G(PREFIX),SUF=$G(SUFIX)
 S Y=-1,PRMPT=PREF_$$EZBLD^DIALOG(1250000.104)_SUF
 W !,PRMPT R X:$S($D(DTIME):DTIME,1:300) I "^"[X!($G(X)="") S Y=-1 Q "^"
 I X="?" D CH1 S %=$$GETCLIN^GMPLAPI5(.LSTS) D PRINTALL^GMPLBLD2(.LSTS,1)
 I X?1"??".E D
 . I X="??" D CH2 S %=$$GETCLIN^GMPLAPI5(.LSTS) D PRINTALL^GMPLBLD2(.LSTS,1)
 E  D:X'="?"
 . S %=$$GETCLIN^GMPLAPI5(.LSTS,X)
 . S Y=$$SELLST^GMPLBLD2(.LSTS,X)
 G:Y<0 CL
 Q Y
 ;
CH1 ;
 N MSG
 D BLD^DIALOG(1250000.105,,,"MSG")
 D EN^DDIOL(.MSG)
 Q
 ;
CH2 ;
 N MSG
 D BLD^DIALOG(1250000.106,,,"MSG")
 D EN^DDIOL(.MSG)
 Q
 ;
USERS(ADD) ; -- select user(s) to de-/assign list
 N DIR,DIC,DIE,DR,DA,X,Y,GMPLUSER,GMPLI,RETURN,PRMT,MSG,PARM
 Q:+$G(GMPLSLST)'>0  S GMPLUSER=""
 S PRMT=$$EZBLD^DIALOG(1250000.107)
 F  D READUS(PRMT) Q:+Y'>0  S GMPLUSER=GMPLUSER_U_+Y,PRMT=$$EZBLD^DIALOG(1250000.108)
 I '$L(GMPLUSER) D  Q
 . D BLD^DIALOG(1250000.109,,,"MSG")
 . D EN^DDIOL(.MSG)
 S DIR(0)="YA",DIR("A")=$$EZBLD^DIALOG(1250000.110),DIR("B")="NO"
 S PARM(1)=$P(GMPLSLST,U,2)
 S PARM(2)=$L(GMPLUSER,U)-1
 D:ADD BLD^DIALOG(1250000.111,.PARM,,"DIR(""?"")")
 D:'ADD BLD^DIALOG(1250000.112,.PARM,,"DIR(""?"")")
 D ^DIR Q:'Y
USR ;
 K MSG
 D:ADD BLD^DIALOG(1250000.113,$P(GMPLSLST,U,2),,"MSG")
 D:'ADD BLD^DIALOG(1250000.114,$P(GMPLSLST,U,2),,"MSG")
 D EN^DDIOL(.MSG)
 F GMPLI=1:1:$L(GMPLUSER,U) S DA=$P(GMPLUSER,U,GMPLI) I DA D
 . S %=$S(ADD:$$ASSUSR^GMPLAPI6(.RETURN,GMPLSLST,DA),1:$$REMUSR^GMPLAPI6(.RETURN,GMPLSLST,DA))
 . D EN^DDIOL($$PROVNAME^GMPLEXT(DA),,"!?4")
 D EN^DDIOL($$EZBLD^DIALOG(1250000.115),"","!!")
 Q
 ;
READUS(PRMT) ; prompt for username, respond
READ ;
 N LSTS,MSG
 D EN^DDIOL(PRMT) R X:DTIME I '$T!("^"[X) S Y=-1 Q
 I X="?" D  G READ
 . D:ADD BLD^DIALOG(1250000.116,,,"MSG")
 . D:'ADD BLD^DIALOG(1250000.117,,,"MSG")
 . D EN^DDIOL(.MSG)
 I X?1"??".E D  G READ
 . I X="??" D
 . . S %=$$GETASUSR^GMPLAPI5(.LSTS,+GMPLSLST)
 . . K MSG
 . . D BLD^DIALOG(1250000.118,$P(GMPLSLST,U,2),,"MSG")
 . . D EN^DDIOL(.MSG)
 . E  D
 . . S %=$$GETUSRS^GMPLAPI5(.LSTS)
 . D PRINTALL^GMPLBLD2(.LSTS,1)
 S %=$$GETUSRS^GMPLAPI5(.LSTS,X)
 S Y=$$SELLST^GMPLBLD2(.LSTS,X) G:Y'>0 READ
 Q
 ;
DELETE ; Delete Selection List
 N DIR,DIK,DA,X,Y,VIEW,USER,GMPCOUNT,GMPQUIT,GMPLSLST,RETURN,MSG
 S GMPCOUNT=0,GMPLSLST=$$LIST^GMPLBLD2("") Q:GMPLSLST=0
 D BLD^DIALOG(1250000.119,,,"MSG")
 D EN^DDIOL(.MSG)
 S %=$$LSTUSED^GMPLAPI1(.RETURN,GMPLSLST)
 S GMPCOUNT=RETURN
 I GMPCOUNT D  Q
 . K MSG
 . D BLD^DIALOG(1250000.120,GMPCOUNT,,"MSG")
 . D EN^DDIOL($C(7))
 . D EN^DDIOL(.MSG)
 D EN^DDIOL($$EZBLD^DIALOG(1250000.121))
DEL1 S DIR(0)="Y",DIR("B")="NO"
 D BLD^DIALOG(1250000.122,,,"DIR(""A"")")
 D BLD^DIALOG(1250000.123,,,"DIR(""?"")")
 W $C(7),! D ^DIR Q:'Y
 K MSG
 D BLD^DIALOG(1250000.124,$P(GMPLSLST,U,2),,"MSG")
 D EN^DDIOL(.MSG)
 K RETURN
 S %=$$DELLST^GMPLAPI1(.RETURN,+GMPLSLST)
 D EN^DDIOL(".",,"")
 D EN^DDIOL($$EZBLD^DIALOG(1250000.115))
 Q
 ;
MENU ; -- init variables and list array for GMPL LIST MENU list template
 ;    Expects GMPLSLST=selection list
 N RETURN,GRP,IND,IND1,MSG
 S IND=0,IND1=0
 S %=$$GETLIST^GMPLAPI1(.RETURN,GMPLSLST,"")
 M ^TMP("GMPLMENU1",$J)=RETURN
 F  S IND=$O(^TMP("GMPLMENU1",$J,"GRP",IND)) Q:'IND  D
 . S GRP=^TMP("GMPLMENU1",$J,"GRP",IND)
 . S ^TMP("GMPLMENU",$J,IND,0)=$P(^TMP("GMPLMENU1",$J,GRP),U,3,4)
 . F  S IND1=$O(^TMP("GMPLMENU1",$J,"GRP",IND,IND1)) Q:'IND1  D
 . . S PRB=^TMP("GMPLMENU1",$J,"GRP",IND,IND1)
 . . S ^TMP("GMPLMENU",$J,IND,IND1)=$P(PRB,U,4)_U_$P(PRB,U,1,2)
 K ^TMP("GMPLMENU1",$J)
 I '$D(^TMP("GMPLMENU",$J)) D  H 2 S VALMBCK="Q",VALMQUIT=1 Q
 . D BLD^DIALOG(1250000.125,,,"MSG")
 . D EN^DDIOL(.MSG)
 D BUILD^GMPLMENU
 Q
