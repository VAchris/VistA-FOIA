GMPLBLD2 ; SLC/MKB,JFR -- Bld PL Selection Lists cont ; 04/12/12
 ;;2.0;Problem List;**3,28,260002**;Aug 25, 1994
 ;
 ; This routine invokes IA #3991
 ;
NEWGRP ; Change problem groups
 N NEWGRP,RETURN,MSG D FULL^VALM1
 I $D(GMPLSAVE),$$CKSAVE D SAVE
NG1 S NEWGRP=$$GROUP("L") G:+NEWGRP'>0 NGQ G:+NEWGRP=+GMPLGRP NGQ
 I '$$LOCKCAT^GMPLAPI1(.RETURN,NEWGRP) D  G NG1
 . D EN^DDIOL($C(7))
 . D BLD^DIALOG(1250000.073,,,"MSG")
 . D EN^DDIOL(.MSG)
 D UNLKCAT^GMPLAPI1(+GMPLGRP) S GMPLGRP=NEWGRP
 D GETLIST^GMPLBLDC,BUILD^GMPLBLDC("^TMP(""GMPLIST"",$J)",GMPLMODE),HDR^GMPLBLDC
NGQ S VALMBCK="R",VALMSG=$$MSG^GMPLX
 Q
 ;
GROUP(L) ;
 N PRMPT,LNAME,X,LSTS,Y,RETURN,LSTS,FILE,FIELDS
GR ;
 S Y=-1,PRMPT=$$EZBLD^DIALOG(1250000.074)
 S FILE=$$EZBLD^DIALOG(1250000.075),FIELDS="NAME"
 W !,PRMPT R X:$S($D(DTIME):DTIME,1:300) I "^"[X!($G(X)="") S Y=-1 Q "^"
 I X="?" S %=$$GETCATS^GMPLAPI5(.LSTS) I $$LSTSH1(.LSTS,FILE,FIELDS) D PRINTALL(.LSTS) D:$L(L)>0 CH1
 I X?1"??".E D
 . I X="??" S %=$$GETCATS^GMPLAPI5(.LSTS) D PRINTALL(.LSTS) D:$L(L)>0 CH2
 E  D:X'="?"
 . S %=$$GETCATS^GMPLAPI5(.LSTS,X)
 . S Y=$$SELLST(.LSTS,X)
 G:Y<0 GR I Y=0,$L(L)'>0 W " ??",! G GR
 I Y=0 D
 . I $L(X)>30!(X?.N)!($L(X)<3)!'(X'?1P.E) W " ??" G GR
 . S ADD=$$ASKADD(X,$$EZBLD^DIALOG(1250000.075)) G:ADD=0 GR
 . S %=$$NEWCAT^GMPLAPI1(.RETURN,X) S Y=RETURN G:RETURN=0 GR
 S:Y<0 Y="^"
 Q Y
 ;
CH1 ;
 N MSG
 D BLD^DIALOG(1250000.076,,,"MSG")
 D EN^DDIOL(.MSG)
 Q
 ;
CH2 ;
 N MSG
 D BLD^DIALOG(1250000.077,,,"MSG")
 D EN^DDIOL(.MSG)
 Q
 ;
NEWLST ; Change selection lists
 N NEWLST,RETURN,MSG D FULL^VALM1
 I $D(GMPLSAVE),$$CKSAVE D SAVE
NL1 S NEWLST=$$LIST("L") G:+NEWLST'>0 NLQ G:+NEWLST=+GMPLSLST NLQ
 I '$$LOCKLST^GMPLAPI1(.RETURN,NEWLST) D  G NL1
 . D EN^DDIOL($C(7))
 . D BLD^DIALOG(1250000.078,,,"MSG")
 . D EN^DDIOL(.MSG)
 D UNLKLST^GMPLAPI1(+GMPLSLST) S GMPLSLST=NEWLST
 D GETLIST^GMPLBLD,BUILD^GMPLBLD("^TMP(""GMPLIST"",$J)",GMPLMODE),HDR^GMPLBLD
NLQ S VALMBCK="R",VALMSG=$$MSG^GMPLX
 Q
 ;
LIST(L) ;
 N PRMPT,LNAME,X,LSTS,Y,RETURN,LSTS,ADD,CLINIC,FILE,FIELDS
LS ;
 S Y=-1,PRMPT=$$EZBLD^DIALOG(1250000.079)
 S FILE="PROBLEM SELECTION LIST",FIELDS=$$EZBLD^DIALOG(1250000.080)
 W !,PRMPT R X:$S($D(DTIME):DTIME,1:300) I "^"[X!($G(X)="") S Y=-1 Q "^"
 I X="?" S %=$$GETLSTS^GMPLAPI5(.LSTS) I $$LSTSH1(.LSTS,FILE,FIELDS) D PRINTALL(.LSTS,1) D:$L(L)>0 LH1
 I X?1"??".E D
 . I X="??" S %=$$GETLSTS^GMPLAPI5(.LSTS) D PRINTALL(.LSTS,1) D:$L(L)>0 LH2
 E  D:X'="?"
 . S %=$$GETLSTS^GMPLAPI5(.LSTS,X)
 . S Y=$$SELLST(.LSTS,X)
 G:Y<0 LS I Y=0,$L(L)'>0 W " ??",! G LS
 I Y=0 D
 . I $L(X)>30!(X?.N)!($L(X)<3)!'(X'?1P.E) W " ??",! G LS
 . S ADD=$$ASKADD(X,FILE) G:ADD=0 LS
 . S CLINIC=$$CLINIC^GMPLBLD3("   "_FILE_" ") S:CLINIC="^" CLINIC=""
 . S %=$$NEWLST^GMPLAPI1(.RETURN,X,CLINIC) S Y=RETURN G:RETURN=0 LS
 S:Y<0 Y="^"
 Q Y
 ;
LH1 ;
 N MSG
 D BLD^DIALOG(1250000.081,,,"MSG")
 D EN^DDIOL(.MSG)
 Q
 ;
LH2 ;
 N MSG
 D BLD^DIALOG(1250000.082,,,"MSG")
 D EN^DDIOL(.MSG)
 Q
 ;
SELLST(LSTS,X) ;
 N CNT,Y,MAXP,CLINE,SEL,OUT,RE
 I $D(LSTS)=0 Q -1
 S CNT=$P(LSTS(0),U,1)
 Q:CNT=0 0
 I CNT=1 W $E(LSTS(1,"NAME"),$L(X)+1,$L(LSTS(1,"NAME"))) Q LSTS(1,"ID")_U_LSTS(1,"NAME")
 S MAXP=5,CLINE=1,SEL=0,OUT=0,RE=0
 F IND=1:1:CNT  D  Q:OUT
 . S CLINE=CLINE+1
 . D EN^DDIOL($C(9)_IND_$C(9)_LSTS(IND,"NAME"))
 . I CLINE>MAXP D
 . . D EN^DDIOL($$EZBLD^DIALOG(1250000.083))
 . I CLINE>MAXP!(IND=CNT) D
 . . S RE=1
 . . D EN^DDIOL($$EZBLD^DIALOG(1250000.084,IND))
 . . R SEL:$S($D(DTIME):DTIME,1:300) S:'$T OUT=1 Q
 . I RE D  Q
 . . S RE=0
 . . I SEL="^" S OUT=1 Q
 . . I $G(SEL)="" S CLINE=1 Q
 . . I $G(SEL)>IND S SEL=0,OUT=1 Q
 . . E  S OUT=1 Q
 Q:SEL>0 LSTS(SEL,"ID")_U_LSTS(SEL,"NAME") Q -1
 ;
PRINTALL(LSTS,CHOOSE) ;
 N IND,CNT,GMPQUIT,LINE
 S GMPQUIT=0,LINE=1
 I $D(LSTS)=0 Q
 S CNT=$P(LSTS(0),U,1) Q:CNT=0
 D:$G(CHOOSE) EN^DDIOL($$EZBLD^DIALOG(1250000.085))
 F IND=1:1:CNT D
 . S LINE=LINE+1
 . D EN^DDIOL("   "_LSTS(IND,"NAME"))
 . I LINE>(IOSL-4) S LINE=1 S:'$$CONTINUE GMPQUIT=1 Q:$D(GMPQUIT)  Q
 D EN^DDIOL("")
 Q
 ;
CONTINUE() ; -- end of page prompt
 N DIR,X,Y
 S DIR(0)="E"
 D BLD^DIALOG(1250000.086,,,"DIR(""A"")")
 D ^DIR
 Q +Y
 ;
LSTSH1(LSTS,FILE,FIELDS) ; All items ??
 N DIR,X,Y,CNT,PARM
 S CNT=$P(LSTS(0),U,1) Q:CNT=0 1
 S PARM(1)=FILE
 S PARM(2)=FIELDS
 D EN^DDIOL($$EZBLD^DIALOG(1250000.087,.PARM))
 Q:CNT<(IOSL-4) 1
 I CNT>(IOSL-4) D
 . S PARM(1)=CNT
 . S PARM(2)=FILE
 . D BLD^DIALOG(1250000.088,.PARM,,"DIR(""A"")")
 S DIR(0)="YO"
 D ^DIR Q Y
 ;
ASKADD(NEWEL,FILE) ; Ask
 N DIR,X,Y,CNT,PARM
 S CNT=$P(LSTS(0),U,1)
 S PARM(1)=NEWEL
 S PARM(2)=FILE
 D BLD^DIALOG(1250000.089,.PARM,,"DIR(""A"")")
 S DIR(0)="YO",DIR("B")="No"
 D ^DIR Q Y
 ;
LAST(ROOT) ; Returns last subscript
 N I,J S (I,J)=""
 F  S I=$O(@(ROOT_"I)")) Q:I=""  S J=I
 Q J
 ;
CKSAVE() ; Save [changes] ??
 N DIR,X,Y,TEXT S TEXT=$S($D(GMPLGRP):$$EZBLD^DIALOG(1250000.056),1:$$EZBLD^DIALOG(1250000.072))
 D BLD^DIALOG(1250000.090,TEXT,,"DIR(""A"")")
 S DIR("B")="YES"
 D BLD^DIALOG(1250000.091,TEXT,,"DIR(""?"")")
 S DIR(0)="YA" D ^DIR
 Q +Y
 ;
SAVE ; Save changes to group/list
 N GMPLQT,LABEL,DA
 S GMPLQT=0
 I $D(GMPLGRP) D  I GMPLQT Q
 . N ITM,CODE,MSG
 . S ITM=0
 . F  S ITM=$O(^TMP("GMPLIST",$J,ITM)) Q:'ITM!(GMPLQT)  D
 .. S CODE=$P(^TMP("GMPLIST",$J,ITM),U,4) Q:'$L(CODE)
 .. I '$$STATCHK^ICDAPIU(CODE,DT) S GMPLQT=1 Q
 . I 'GMPLQT Q  ;no inactive codes in the category
 . D FULL^VALM1
 . D BLD^DIALOG(1250000.092,,,"MSG")
 . D EN^DDIOL($C(7))
 . D EN^DDIOL(.MSG)
 . N DIR,DUOUT,DTOUT,DIRUT
 . S DIR(0)="E" D ^DIR
 . S VALMBCK="R",GMPLQT=1
 . Q
 ;
 I '$D(GMPLGRP),$D(GMPLSLST) D  I GMPLQT Q
 . N GRP,RET,MSG
 . S GRP=0
 . F  S GRP=$O(^TMP("GMPLIST",$J,"GRP",GRP)) Q:'GRP!(GMPLQT)  D
 .. I $$VALGRP^GMPLAPI6(.RET,GRP) Q  ;no inactive codes in the GROUP
 .. S GMPLQT=1
 . I 'GMPLQT Q  ; all groups and problems OK
 . D FULL^VALM1
 . D BLD^DIALOG(1250000.093,,,"MSG")
 . D EN^DDIOL($C(7))
 . D EN^DDIOL(.MSG)
 . N DIR,DUOUT,DTOUT,DIRUT
 . S DIR(0)="E" D ^DIR
 . S VALMBCK="R",GMPLQT=1
 . Q
 D EN^DDIOL($$EZBLD^DIALOG(1250000.094,"","!!"))
 N SOURCE,RETURN
 M SOURCE=^TMP("GMPLIST",$J)
 S %=$S($D(GMPLGRP):$$SAVGRP^GMPLAPI1(.RETURN,GMPLGRP,.SOURCE),1:$$SAVLST^GMPLAPI1(.RETURN,GMPLSLST,.SOURCE))
 K GMPLSAVE,SOURCE S:$D(GMPLGRP) GMPSAVED=1
 S VALMBCK="Q" D EN^DDIOL($$EZBLD^DIALOG(1250000.095,"","")) H 1
 Q
 ;
DELETE ; Delete problem group
 N DIR,X,Y,DA,DIK,IFN,CNT,RETURN,MSG S VALMBCK=$S(VALMCC:"",1:"R")
 S %=$$CATUSED^GMPLAPI1(.RETURN,+GMPLGRP)
 I RETURN D  H 2 Q
 . D BLD^DIALOG(1250000.096,,,"MSG")
 . D EN^DDIOL($C(7))
 . D EN^DDIOL(.MSG)
 S DIR(0)="YA",DIR("B")="NO"
 D BLD^DIALOG(1250000.097,$P(GMPLGRP,U,2),,"DIR(""A"")")
 D BLD^DIALOG(1250000.098,,,"DIR(""?"")")
 D ^DIR Q:'Y
DEL1 ; Ok, go for it ...
 D EN^DDIOL($$EZBLD^DIALOG(1250000.099),"","!!")
 K RETURN
 S %=$$DELCAT^GMPLAPI1(.RETURN,+GMPLGRP)
 D UNLKCAT^GMPLAPI1(+GMPLGRP) S GMPLGRP=0 K GMPLSAVE
 D EN^DDIOL($$EZBLD^DIALOG(1250000.100),"","")
 D NEWGRP S:+GMPLGRP'>0 VALMBCK="Q"
 Q
 ;
ASSIGN ; allow lookup of PROB SEL LIST and assign to users
 ;
 N DIC,X,Y,DUOUT,DTOUT,GMPLSLST,RET,MSG
 S Y=$$LIST("")
 Q:Y'>0
 I '$$VALLIST^GMPLAPI6(.RET,+Y) D  G ASSIGN
 . D BLD^DIALOG(1250000.093,,,"MSG")
 . D EN^DDIOL($C(7))
 . D EN^DDIOL(.MSG)
 S GMPLSLST=+Y
 D USERS^GMPLBLD3("1")
 Q
