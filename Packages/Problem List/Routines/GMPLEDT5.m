GMPLEDT5 ; RGI -- Edit Site parameters ; 04/06/12
 ;;2.0;Problem List;**260002**;Aug 25, 1994
 Q  ;
 ;
CHOOSE(VALUE,PARAMS) ;
 N DIR,X,X1,Y,IND,OLDVAL,VAL,KEY,SEP,LCNT,HCNT,HIND
CH ; for recursive call
 S DIR(0)="SAO^",SEP="",IND="",OLDVAL=""
 S DIR("L",1)=$$EZBLD^DIALOG(1250000.282),LCNT=1
 F  S IND=$O(PARAMS("OPT",IND)) Q:IND=""  D
 . S KEY=$P(PARAMS("OPT",IND),U,1)
 . S VAL=$P(PARAMS("OPT",IND),U,2)
 . S LCNT=LCNT+1
 . S DIR("L",LCNT)="       "_KEY_"        "_VAL
 . S DIR(0)=DIR(0)_SEP_KEY_":"_VAL
 . S SEP=";"
 S IND=0
 S DIR("L")=DIR("L",LCNT) K DIR("L",LCNT)
 F  S IND=$O(PARAMS("OPT",IND)) Q:IND=""  D
 . I $P(PARAMS("OPT",IND),U,1)=VALUE S OLDVAL=$P(PARAMS("OPT",IND),U,2) Q
 S HCNT=0,HIND=""
 F  S HIND=$O(PARAMS("H1",HIND)) Q:HIND=""  D
 . S HCNT=HCNT+1
 . S DIR("?",HCNT)=PARAMS("H1",HIND)
 S DIR("?")=PARAMS("H1")
 S DIR("??")=PARAMS("H2")
 S DIR("B")=OLDVAL
 S DIR("A")=PARAMS("FLD")_": "
 D ^DIR
 I X="@" S X1=X I $$ASKDEL() S Y=X1  E  G CH
 I $G(DUOUT),$G(DIRUT),Y'="^",'$G(DIROUT) G CH
 Q Y
 ;
ASKDEL() ;
 S DIR(0)="YO"
 D BLD^DIALOG(1250000.283,,,"DIR(""A"")")
 D BLD^DIALOG(1250000.284,,,"DIR(""?"")")
 D ^DIR
 Q Y
 ;
SELPAR(FIELD,VALUE) ;
 N PARAMS,DIR S PARAMS=""
 I FIELD="VER" D
 . S PARAMS("OPT",1)="1^YES"
 . S PARAMS("OPT",2)="0^NO"
 . S PARAMS("FLD")=$$EZBLD^DIALOG(1250000.285)
 . D BLD^DIALOG(1250000.286,,,"DIR(""?"")")
 I FIELD="PRT" D
 . S PARAMS("OPT",1)="1^YES, ASK"
 . S PARAMS("OPT",2)="0^NO, DON'T ASK"
 . S PARAMS("FLD")=$$EZBLD^DIALOG(1250000.287)
 . D BLD^DIALOG(1250000.288,,,"DIR(""?"")")
 I FIELD="CLU" D
 . S PARAMS("OPT",1)="1^YES"
 . S PARAMS("OPT",2)="0^NO"
 . S PARAMS("FLD")=$$EZBLD^DIALOG(1250000.289)
 . D BLD^DIALOG(1250000.290,,,"DIR(""?"")")
 I FIELD="REV" D
 . S PARAMS("OPT",1)="C^CHRONOLOGICAL"
 . S PARAMS("OPT",2)="R^REVERSE-CHRONOLOGICAL"
 . S PARAMS("FLD")=$$EZBLD^DIALOG(1250000.291)
 . D BLD^DIALOG(1250000.292,,,"DIR(""?"")")
 I FIELD="SDP" D
 . S PARAMS("OPT",1)="1^YES"
 . S PARAMS("FLD")=$$EZBLD^DIALOG(1250000.293)
 . D BLD^DIALOG(1250000.294,,,"DIR(""?"")")
 M:$D(DIR("?")) PARAMS("H1")=DIR("?")
 S PARAMS("H2")="^D PARHLP^GMPLEDT5("""_FIELD_""",.PARAMS)"
 S NEWVAL=$$CHOOSE(VALUE,.PARAMS)
 Q NEWVAL
 ;
PARHLP(FIELD,PARAMS) ;
 N MSG
 I FIELD="VER" D
 . D BLD^DIALOG(1250000.295,,,"MSG")
 I FIELD="PRT" D
 . D BLD^DIALOG(1250000.296,,,"MSG")
 I FIELD="CLU" D
 . D BLD^DIALOG(1250000.297,,,"MSG")
 I FIELD="REV" D
 . D BLD^DIALOG(1250000.298,,,"MSG")
 I FIELD="SDP" D
 . D BLD^DIALOG(1250000.299,,,"MSG")
 D:$D(MSG) EN^DDIOL(.MSG)
 D EN^DDIOL($$EZBLD^DIALOG(1250000.244),,"!?5")
 N IND S IND=""
 F  S IND=$O(PARAMS("OPT",IND)) Q:IND=""  D
 . D EN^DDIOL($P(PARAMS("OPT",IND),U,1)_"        "_$P(PARAMS("OPT",IND),U,2),,"!?7")
 Q
 ;
