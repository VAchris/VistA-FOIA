GMPLMENU ; SLC/MKB -- VALM Utilities for Add Menu sub-list ;09/14/12
 ;;2.0;Problem List;**11,260002**;Aug 25, 1994
HDR ; -- header code
 N PAT,NUM,LIST S NUM=$$EZBLD^DIALOG(1250000.308,GMPLCNT)
 S PAT=$P(GMPDFN,U,2)_"  ("_$P(GMPDFN,U,3)_")"
 S VALMHDR(1)=PAT_$J(NUM,79-$L(PAT)),LIST=$P(GMPLSLST,U,2)
 S VALMHDR(2)=$J(LIST,$L(LIST)\2+41)
 Q
 ;
HELP ; -- help code
 N CNT,MSG,DIR S CNT=+$G(^TMP("GMPLMENU",$J,"LIST",0))
 D BLD^DIALOG(1250000.309,CNT,,"MSG")
 D EN^DDIOL(.MSG)
 I VALMCNT>10 D EN^DDIOL($$EZBLD^DIALOG(1250000.310))
 S DIR(0)="EA"
 D BLD^DIALOG(1250000.311,,,"DIR(""A"")")
 D ^DIR
 S VALMSG=$$MSG,VALMBCK=$S(VALMCC:"",1:"R")
 Q
EXIT ; -- exit code
 N I F I=0:0 S I=$O(XQORM("KEY",I)) Q:I'>0  K XQORM("KEY",I)
 K ^TMP("GMPLMENU",$J),GMPLCNT
 Q
 ;
MSG() ; -- set LMgr msg bar
 Q $$EZBLD^DIALOG(1250000.312)
 ;
BUILD ; -- Build ^TMP("GMPLMENU",$J,"LIST") list to display
 N I,LCNT,NUM,ITEM,CODE,GRP,PROBS,ADDED
 S (GRP,NUM,LCNT)=0 D CLEAN^VALM10
 F  S GRP=$O(^TMP("GMPLMENU",$J,GRP)) Q:GRP'>0  D
 . S ITEM=$G(^TMP("GMPLMENU",$J,GRP,0)),PROBS=+$P(ITEM,U,2)
 . I 'PROBS D  Q
 . . S LCNT=LCNT+1,NUM=NUM+1,^TMP("GMPLMENU",$J,"IDX",NUM)=U_GRP_U_LCNT
 . . S ^TMP("GMPLMENU",$J,"LIST",LCNT,0)=$J(NUM,5)_" "_$P(ITEM,U)_" ..."
 . . D CNTRL^VALM10(LCNT,1,5,IOINHI,IOINORM)
BLD1 . I LCNT,^TMP("GMPLMENU",$J,"LIST",LCNT,0)'="   " S LCNT=LCNT+1,^TMP("GMPLMENU",$J,"LIST",LCNT,0)="   "
 . S:+$G(GMPLGRP)=GRP VALMBG=LCNT+1 ; start list display here
 . I $L($P(ITEM,U)) D  ; have a hdr
 . . S LCNT=LCNT+1,^TMP("GMPLMENU",$J,"LIST",LCNT,0)="      "_$P(ITEM,U)
 . . D CNTRL^VALM10(LCNT,7,$L($P(ITEM,U)),IOUON,IOUOFF)
 . S I=0 F  S I=$O(^TMP("GMPLMENU",$J,GRP,I)) Q:I'>0  D
 . . S LCNT=LCNT+1,NUM=NUM+1
 . . S ITEM=$G(^TMP("GMPLMENU",$J,GRP,I)),CODE=$P(ITEM,U,3),ADDED=+$P(ITEM,U,4) ; ITEM=term^text^code, _"^1" if added
 . . S ^TMP("GMPLMENU",$J,"LIST",LCNT,0)=$S(ADDED:" X",1:"  ")_$J(NUM,3)_" "_$P(ITEM,U,2)_$S($L(CODE):" ("_CODE_")",1:"")
 . . S ^TMP("GMPLMENU",$J,"IDX",NUM)=I_U_GRP_U_LCNT_U_ITEM
 . . D CNTRL^VALM10(LCNT,1,5,IOINHI,IOINORM)
 . S LCNT=LCNT+1,^TMP("GMPLMENU",$J,"LIST",LCNT,0)="   "
BLDQ S ^TMP("GMPLMENU",$J,"LIST",0)=NUM_U_LCNT,VALMCNT=LCNT,GMPLCNT=0,VALMSG=$$MSG
 D KEYS
 Q
 ;
KEYS ; -- setup XQORM("KEY") array for menu
 N I,PROTCL,NUM S NUM=+$G(^TMP("GMPLMENU",$J,"LIST",0))
 S PROTCL=$$PROTKEY^GMPLEXT("GMPL LIST SELECT ITEM")_"^1"
 F I=1:1:NUM S XQORM("KEY",I)=PROTCL
 S VALMSG=$$MSG
 Q
 ;
CK ; -- check whether to stop processing after each problem
 ; Called from exit action of GMPL LIST XXX protocols
 S:$D(GMPQUIT) XQORPOP=1 K GMPQUIT
 I $D(DTOUT)!($G(VALMBCK)="Q") S VALMBCK="Q" Q
 S VALMBCK="R",VALMSG=$$MSG
 Q
 ;
ITEM ; -- select item from menu
 N NUM,GMPROB,GMPTERM,GMPICD,GMPSAVED,LCNT,LINE,DUP,ITEM,CODE,GRP,PROB,GMPINDEX,MSG,PARM
 S NUM=+$P(XQORNOD(0),U,3) Q:NUM'>0
 S GMPINDEX=$G(^TMP("GMPLMENU",$J,"IDX",NUM)),PROB=+GMPINDEX,GRP=$P(GMPINDEX,U,2)
 I 'PROB D  Q  ; expand category
 . S ITEM=$G(^TMP("GMPLMENU",$J,+GRP,0)) S:'$D(GMPLGRP) GMPLGRP=+GRP
 . S ^TMP("GMPLMENU",$J,+GRP,0)=$P(ITEM,U)_"^1"
 S ITEM=$P(GMPINDEX,U,4,6) ; CLU^text^code
 S GMPTERM=$S(+ITEM>1:$P(ITEM,U,1,2),1:""),GMPROB=$P(ITEM,U,2)
 S CODE=$P(ITEM,U,3),GMPICD=$S('$L(CODE):"799.9",1:CODE)
 S PARM(1)=NUM,PARM(2)=GMPROB
 D BLD^DIALOG(1250000.313,.PARM,,"MSG")
 D EN^DDIOL(.MSG)
 N %
 S %=$$DUPL^GMPLAPI2(.DUP,+GMPDFN,+GMPTERM,GMPROB,0)
 I DUP,'$$DUPLOK^GMPLX(DUP) G ITQ
 D ADD1^GMPL1
ITQ I $D(GMPSAVED) D  D HDR
 . S GMPREBLD=1,GMPLCNT=GMPLCNT+1,LCNT=+$P(GMPINDEX,U,3)
 . S LINE=$G(^TMP("GMPLMENU",$J,"LIST",LCNT,0)),^TMP("GMPLMENU",$J,"LIST",LCNT,0)=" X"_$E(LINE,3,999)
 . S ^TMP("GMPLMENU",$J,+GRP,+PROB)=ITEM_"^1" ; problem added
 Q
 ;
CLU ; -- add problem not on menu, from CLU
 N GMPSAVED,MSG
 D BLD^DIALOG(1250000.314,,,"MSG")
 D EN^DDIOL(.MSG)
 W @IOF D FULL^VALM1,ADD^GMPL1 S VALMBCK="R" I $D(GMPSAVED) S GMPREBLD=1,GMPLCNT=GMPLCNT+1 K VALMHDR
 Q
