GMPLAPI2 ; / Problem List API - NEW,DELETE,VERIFY,VIEW DETAILED INFO,ADD NOTE ;02/27/12  12:00
 ;;TBD;Problem List;;02/27/2012
NEW(GMPDFN,GMPROV,GMPVAMC,GMPFLD,ERT) ; Save Collected Values in new Problem Entry
 ; Input
 ;  GMPDFN (R)   
 ;  GMPROV (R)  
 ;  GMPVAMC (R) 
 ;  GMPFLD (R)  
 ;  ERT         
 ; Output: IEN of PROBLEM file 
 I '$G(GMPDFN) D ERR^GMPLAPIE(ERT,"INVALIDPARAM","GMPDFN") Q 0
 I '$G(GMPVAMC) D ERR^GMPLAPIE(ERT,"INVALIDPARAM","GMPVAMC") Q 0
 N GMPIFN,NOTES
 I $P($G(GMPFLD(1.14)),U)="0" D
 . I '$D(GMPFLD(1.18)) S GMPFLD(1.18)=GMPFLD(1.14)
 . S GMPFLD(1.14)=""
 S:'$G(GMPFLD(.01)) GMPFLD(.01)=$$NOS^GMPLEXT
 S:$P(+GMPFLD(.01),U)=-1 GMPFLD(.01)=$$NOS^GMPLEXT ;chk for error from ICD
 S GMPFLD(.01)=+GMPFLD(.01) ;to remove text left by ?? lex (~)
 S:'$G(GMPFLD(1.01)) GMPFLD(1.01)="1^Unresolved"
 S:'$G(GMPFLD(.05)) GMPFLD(.05)=$$PROVNARR^GMPLEXT($P($G(GMPFLD(.05)),U,2),+GMPFLD(1.01))
 S GMPIFN=$$CREATE^GMPLDAL(GMPDFN,GMPVAMC,.GMPFLD,ERT)
 I $D(GMPFLD(10,"NEW"))>9 D 
 . M NOTES=GMPFLD(10,"NEW")
 . D NEWNOTE^GMPLAPI3(GMPIFN,GMPROV,GMPVAMC,.NOTES,ERT)
 Q GMPIFN
 ;
UPDATE(GMPIFN,GMPORIG,GMPFLD,GMPLUSER,GMPVAMC,GMPROV,ERT) ; Save Changes made to Existing Problem
 ; GMPIFN
 ; GMPORIG
 ; GMPFLD
 ; GMPLUSER
 ; GMPVAMC
 ; GMPROV
 ; ERT
 N I,NIFN,TEXT,OLDTEXT,FAC,NOTES,GMPSAVED
 S:$P($G(GMPFLD(1.14)),U)="0" GMPFLD(1.14)=""
 S:'GMPORIG(.01) GMPORIG(.01)=$$NOS^GMPLEXT
 S:'GMPFLD(.01) GMPFLD(.01)=$$NOS^GMPLEXT
 S:$D(GMPFLD(.01)) GMPFLD(.01)=+GMPFLD(.01)
 S:$P(+GMPFLD(.01),U)=-1 GMPFLD(.01)=$$NOS^GMPLEXT ;chk for error from ICD
 S:'GMPORIG(1.01) GMPORIG(1.01)="1^Unresolved"
 S:'GMPFLD(1.01) GMPFLD(1.01)="1^Unresolved"
 S:'GMPFLD(.05) I=$P(GMPFLD(.05),U,2),GMPFLD(.05)=$$PROVNARR^GMPLEXT(I,+GMPFLD(1.01))
 S GMPFLD(.01)=+GMPFLD(.01) ;to remove text left by ?? lex (~)
 S GMPSAVED=$$UPDATE^GMPLDAL(GMPIFN,GMPORIG,GMPFLD,GMPLUSER,GMPROV,ERT)
 ; Save Changes to Notes
 F I=0:0 S I=$O(GMPORIG(10,I)) Q:I'>0  D UPDNOTE^GMPLAPI3(GMPIFN,GMPORIG(10,I),GMPFLD(10,I),GMPROV,ERT)
 I $D(GMPFLD(10,"NEW"))>9 D 
 . M NOTES=GMPFLD(10,"NEW")
 . D NEWNOTE^GMPLAPI3(GMPIFN,GMPROV,GMPVAMC,.NOTES,ERT)
 ; Quit Saving Changes
 D:GMPSAVED DTMOD^GMPLDAL(GMPIFN)
 Q
 ;
DELETE(GMPIFN,GMPROV,GMPVAMC,REASON,ERT) ; DELETE A PROBLEM
 ; Input   GMPIFN   IEN of PROBLEM file
 ;         GMPROV   pointer to NEW PERSON file
 ;         GMPVAMC  pointer to LOCATION file
 ;         REASON   comment
 ;         ERT      Error array root
 S ERT=$G(ERT)
 I $G(GMPIFN)="" D ERR^GMPLAPIE(ERT,"INVALIDPARAM","GMPIFN") Q 0 ; Invalid parameter value
 I $$COND(GMPIFN)="H" D ERR^GMPLAPIE(ERT,"PRBDELETED") Q 0 ; Already deleted
 I $G(GMPVAMC)="" D ERR^GMPLAPIE(ERT,"INVALIDPARAM","GMPVAMC") Q 0 ; Invalid parameter value
 I $G(REASON)'="" D NEWNOTE^GMPLAPI3(GMPIFN,GMPROV,GMPVAMC,REASON,ERT)
 Q $$DELETE^GMPLDAL(GMPIFN,GMPROV,ERT)
 ;
DETAIL(GMPIFN,GMPL,GMPLMGR,GMPVAMC,GMPROV) ; 
 N DIC,DIQ,DR,I,CNT,NIFN,FAC,EXT
 D DETAIL^GMPLDALF(GMPIFN,.GMPL,"")
 F I=.03,.08,.13,1.07,1.09 S $P(GMPL(I),U,2)=$$EXTDT^GMPLX($P(GMPL(I),U))
 S $P(GMPL(1.11),U,2)=$S($P(GMPL(1.11),U):"AGENT ORANGE",1:"")
 S $P(GMPL(1.12),U,2)=$S($P(GMPL(1.12),U):"RADIATION",1:"")
 S $P(GMPL(1.13),U,2)=$S($P(GMPL(1.13),U):"ENV CONTAMINANTS",1:"")
 S $P(GMPL(1.15),U,2)=$S($P(GMPL(1.15),U):"HEAD/NECK CANCER",1:"")
 S $P(GMPL(1.16),U,2)=$S($P(GMPL(1.16),U):"MIL SEXUAL TRAUMA",1:"")
 S $P(GMPL(1.17),U,2)=$S($P(GMPL(1.17),U):"COMBAT VET",1:"")
 S $P(GMPL(1.18),U,2)=$S($P(GMPL(1.18),U):"SHAD",1:"")
 ;COMMENTS
 S CNT=0
 S GMPL(10,0)=CNT ;(CNT,GMPORIG(10,0),GMPFLD(10,0))=0
 S FAC=$O(^AUPNPROB(GMPIFN,11,"B",+GMPVAMC,0)) Q:'FAC 1
 F NIFN=0:0 S NIFN=$O(^AUPNPROB(GMPIFN,11,FAC,11,"B",NIFN)) Q:NIFN'>0  D
 . I $G(GMPLMGR)'="",$P($G(^AUPNPROB(GMPIFN,11,FAC,11,NIFN,0)),U,6)'=+GMPROV Q
 . S CNT=CNT+1,GMPL(10,CNT)=$G(^AUPNPROB(GMPIFN,11,FAC,11,NIFN,0))
 . S $P(GMPL(10,CNT),U,2)=FAC
 . ;S GMPFLD(10,CNT)=GMPORIG(10,CNT)
 S GMPL(10,0)=CNT
 Q 1
 ;
DETAILX(GMPIFN,GMPL,GMPLMGR,GMPVAMC,GMPROV) ; Returns Formatted Detailed Data for Problem
 ;                
 ; Input   GMPIFN  Pointer to Problem file #9000011
 ;                
 ; Output  GMPL Array, passed by reference
 ;         GMPL("DATA NAME") = External Format of Value
 ;
 ;         GMPL("DIAGNOSIS")  ICD Code
 ;         GMPL("PATIENT")    Patient Name
 ;         GMPL("MODIFIED")   Date Last Modified
 ;         GMPL("NARRATIVE")  Provider Narrative 
 ;         GMPL("ENTERED")    Date Entered ^ Entered by
 ;         GMPL("STATUS")     Status
 ;         GMPL("PRIORITY")   Priority Acute/Chronic
 ;         GMPL("ONSET")      Date of Onset
 ;         GMPL("PROVIDER")   Responsible Provider
 ;         GMPL("RECORDED")   Date Recorded ^ Recorded by
 ;         GMPL("CLINIC")     Hospital Location
 ;         GMPL("SC")         Service Connected SC/NSC/""
 ;
 ;         GMPL("EXPOSURE") = #
 ;         GMPL("EXPOSURE",X)="AGENT ORANGE"
 ;         GMPL("EXPOSURE",X)="RADIATION"
 ;         GMPL("EXPOSURE",X)="ENV CONTAMINANTS"
 ;         GMPL("EXPOSURE",X)="HEAD AND/OR NECK CANCER"
 ;         GMPL("EXPOSURE",X)="MILITARY SEXUAL TRAUMA"
 ;         GMPL("EXPOSURE",X)="COMBAT VET"
 ;         GMPL("EXPOSURE",X)="SHAD"
 ;
 ;         GMPL("COMMENT") = #
 ;         GMPL("COMMENT",CNT) = Date ^ Author ^ Text of Note
 ;              
 N GMPFLD,GMPLP,X,I,FAC,CNT,NIFN,IDT,AIFN
 S GMPFLD=""
 Q:'$$DETAIL(GMPIFN,.GMPFLD,GMPLMGR,GMPVAMC,GMPROV)
 ;S GMPLP=+($$PTR^GMPLUTL4)
 S GMPL("DIAGNOSIS")=$P(GMPFLD(.01),U,2)
 S GMPL("PATIENT")=$P(GMPFLD(.02),U,2)
 S GMPL("MODIFIED")=$P(GMPFLD(.03),U,2)
 S GMPL("NARRATIVE")=$$PROBTEXT^GMPLX(GMPIFN)
 S GMPL("ENTERED")=$P(GMPFLD(.08),U,2)_U_$P(GMPFLD(1.03),U,2)
 S GMPL("STATUS")=$P(GMPFLD(.12),U,2)
 S X=$S($P(GMPFLD(.12),U)'="A":"",1:$P(GMPFLD(1.14),U))
 S GMPL("PRIORITY")=$S(X="A":"ACUTE",X="C":"CHRONIC",1:"")
 S GMPL("ONSET")=$P(GMPFLD(.13),U,2)
 S GMPL("CONDITION")=$P(GMPFLD(1.02),U,2)
 S GMPL("PROVIDER")=$P(GMPFLD(1.05),U,2)
 S GMPL("SERVICE")=$P(GMPFLD(1.06),U,2)
 S GMPL("RESOLVED")=$P(GMPFLD(1.07),U,2)
 S GMPL("RECORDED")=$P(GMPFLD(1.09),U,2)_U_$P(GMPFLD(1.04),U,2)
 S GMPL("CLINIC")=$P(GMPFLD(1.08),U,2)
 S GMPL("SC")=$S($P(GMPFLD(1.10),U)'="":$P(GMPFLD(1.10),U,2),1:"UNKNOWN")
 S GMPL("EXPOSURE")=0
 F I=1.11,1.12,1.13,1.15,1.16,1.17,1.18 I GMPFLD(I) D
 . S X=GMPL("EXPOSURE")+1
 . S GMPL("EXPOSURE",X)=$P(GMPFLD(I),U,2)
 . S GMPL("EXPOSURE")=X
 S (FAC,CNT)=0,GMPL("COMMENT")=0
 F FAC=0:0 S FAC=$O(^AUPNPROB(GMPIFN,11,FAC)) Q:+FAC'>0  D
 . F NIFN=0:0 S NIFN=$O(^AUPNPROB(GMPIFN,11,FAC,11,NIFN)) Q:NIFN'>0  D
 . . S X=$G(^AUPNPROB(GMPIFN,11,FAC,11,NIFN,0))
 . . S CNT=CNT+1
 . . S GMPL("COMMENT",CNT)=$$EXTDT^GMPLX($P(X,U,5))_U_$$PROVNAME^GMPLEXT($P(X,U,6))_U_$P(X,U,3)
 S GMPL("COMMENT")=CNT ;D AUDIT^GMPLUTL2
 S CNT=0
 I $D(^GMPL(125.8,"B",GMPIFN)) D
 . F IDT=0:0 S IDT=$O(^GMPL(125.8,"AD",GMPIFN,IDT)) Q:IDT'>0  D
 . . F AIFN=0:0 S AIFN=$O(^GMPL(125.8,"AD",GMPIFN,IDT,AIFN)) Q:AIFN'>0  D
 . . . S CNT=CNT+1
 . . . S GMPL("HISTORY",CNT)=AIFN
 S GMPL("HISTORY")=CNT
 Q
 ;
VERIFY(GMPIFN,ERT) ; Verify a transcribed problem
 ;
 ; Input   GMPIFN  Pointer to Problem file #9000011
 ;         ERT
 ;
 ; Output 
 S ERT=$G(ERT)
 I $$VERIFIED^GMPLDAL(GMPIFN) D ERR^GMPLAPIE(ERT,"PRBVERIFIED") Q 0 ; BAIL OUT - ALREADY VERIFIED
 Q:'$$LOCK^GMPLDAL(GMPIFN,0,ERT) 0
 D MKPERM^GMPLDAL(GMPIFN)
 D UNLOCK^GMPLDAL(GMPIFN,0)
 Q 1
 ;
DUPL(DFN,TERM,TEXT) ; Check's for Duplicate Entries
 N DA,IFN,NODE0,NODE1
 S DA=0,TEXT=$$UP^XLFSTR(TEXT)
 I '$D(^AUPNPROB("C",TERM))!('$D(^AUPNPROB("AC",DFN))) Q DA
 F IFN=0:0 S IFN=$O(^AUPNPROB("AC",DFN,IFN)) Q:IFN'>0  D  Q:DA>0
 . S NODE0=$G(^AUPNPROB(IFN,0)),NODE1=$G(^(1)) Q:$P(NODE1,U,2)="H"
 . I TERM>1 S:+NODE1=TERM DA=IFN Q
 . S:TEXT=$$UP^XLFSTR($P(^AUTNPOV($P(NODE0,U,5),0),U)) DA=IFN
 Q DA
 ;
CODESTS(PROB,ADATE) ;check status of code associated with a problem
 ; Input:
 ;    PROB  = pointer to the PROBLEM (#9000011) file
 ;    ADATE = FM date on which to check the status of ICD9 code  (opt.) 
 ;
 ; Output:
 ;   1  = ACTIVE on the date passed or current date if not passed
 ;   0  = INACTIVE on the date passed or current date if not passed
 ;
 I '$G(ADATE) S ADATE=DT
 I '$D(^AUPNPROB(PROB,0)) Q 0
 S PROB=$P(^AUPNPROB(PROB,0),U)
 Q +($$STATCHK^ICDAPIU($$CODEC^ICDCODE(+(PROB)),ADATE))
 ;
COND(GMPIFN) ; Returns the condition flag (T,P,H)
 Q $P($G(^AUPNPROB(GMPIFN,1)),U,2)
 ;
STATUS(GMPIFN) ; Returns the status flag (A,I)
 Q $P(^AUPNPROB(GMPIFN,0),U,12)
 ;
ONSET(GMPIFN) ; Returns the date of onset
 Q $P($G(^AUPNPROB(GMPIFN,0)),U,13)
 ;
INACTV(GMPIFN,GMPROV,GMPVAMC,NOTE,RESOLVED,ERT) ; Inactivate problem
 N DA,DR,DIE
 S ERT=$G(ERT)
 I $$STATUS(GMPIFN)="I" D ERR^GMPLAPIE(ERT,"PRBINACTIVE"," ("_$$PROBTEXT^GMPLX(GMPIFN)_")") Q 0
 I $$COND(GMPIFN)="H" D ERR^GMPLAPIE(ERT,"PRBDELETED"," ("_$$PROBTEXT^GMPLX(GMPIFN)_")") Q 0
 D:$G(NOTE)'="" NEWNOTE^GMPLAPI3(GMPIFN,GMPROV,GMPVAMC,NOTE,ERT)
 S DIE="^AUPNPROB(",DR=".12///I;1.07////"_$P($G(RESOLVED),U)
 S DA=GMPIFN D ^DIE
 S CHNGE=GMPIFN_"^.12^"_$$HTFM^XLFDT($H)_U_DUZ_"^A^I^^"_+$G(GMPROV)
 D AUDIT^GMPLX(CHNGE,"")
 D DTMOD^GMPLX(GMPIFN)
 Q 1
 ;
