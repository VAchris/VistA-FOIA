GMPLAPI2 ; / Problem List API - NEW,DELETE,VERIFY,VIEW DETAILED INFO,ADD NOTE ;02/27/12  12:00
 ;;TBD;Problem List;;02/27/2012
NEW(GMPDFN,GMPROV,GMPVAMC,GMPFLD,ERT) ; Save Collected Values in new Problem Entry
 ; Input
 ;  GMPDFN (R)   
 ;  GMPROV (R)  
 ;  GMPVAMC (R) 
 ;  GMPFLD (R)  
 ;  ERT         
 ; Output: IEN of PROBLEM file 
 I '$G(GMPDFN) D ERR^GMPLAPIE(ERT,"INVALIDPARAM","GMPDFN") Q 0
 I '$G(GMPVAMC) D ERR^GMPLAPIE(ERT,"INVALIDPARAM","GMPVAMC") Q 0
 N GMPIFN,NOTES
 I $P($G(GMPFLD(1.14)),U)="0" D
 . I '$D(GMPFLD(1.18)) S GMPFLD(1.18)=GMPFLD(1.14)
 . S GMPFLD(1.14)=""
 S:'$G(GMPFLD(.01)) GMPFLD(.01)=$$NOS^GMPLEXT
 S:$P(+GMPFLD(.01),U)=-1 GMPFLD(.01)=$$NOS^GMPLEXT ;chk for error from ICD
 S GMPFLD(.01)=+GMPFLD(.01) ;to remove text left by ?? lex (~)
 S:'$G(GMPFLD(1.01)) GMPFLD(1.01)="1^Unresolved"
 S:'$G(GMPFLD(.05)) GMPFLD(.05)=$$PROVNARR^GMPLEXT($P($G(GMPFLD(.05)),U,2),+GMPFLD(1.01))
 S GMPIFN=$$CREATE^GMPLDAL(.GMPFLD,ERT)
 I $D(GMPFLD(10,"NEW"))>9 D 
 . M NOTES=GMPFLD(10,"NEW")
 . D NEWNOTE^GMPLAPI3(GMPIFN,GMPROV,GMPVAMC,.NOTES,ERT)
 Q GMPIFN
 ;
UPDATE(GMPIFN,GMPORIG,GMPFLD,GMPLUSER,GMPVAMC,GMPROV,ERT) ; Save Changes made to Existing Problem
 ; GMPIFN
 ; GMPORIG
 ; GMPFLD
 ; GMPLUSER
 ; GMPVAMC
 ; GMPROV
 ; ERT
 N I,NIFN,TEXT,OLDTEXT,FAC,NOTES,GMPSAVED
 S:$P($G(GMPFLD(1.14)),U)="0" GMPFLD(1.14)=""
 S:'GMPORIG(.01) GMPORIG(.01)=$$NOS^GMPLEXT
 S:'GMPFLD(.01) GMPFLD(.01)=$$NOS^GMPLEXT
 S:$D(GMPFLD(.01)) GMPFLD(.01)=+GMPFLD(.01)
 S:$P(+GMPFLD(.01),U)=-1 GMPFLD(.01)=$$NOS^GMPLEXT ;chk for error from ICD
 S:'GMPORIG(1.01) GMPORIG(1.01)="1^Unresolved"
 S:'GMPFLD(1.01) GMPFLD(1.01)="1^Unresolved"
 S:'GMPFLD(.05) I=$P(GMPFLD(.05),U,2),GMPFLD(.05)=$$PROVNARR^GMPLEXT(I,+GMPFLD(1.01))
 S GMPFLD(.01)=+GMPFLD(.01) ;to remove text left by ?? lex (~)
 S GMPSAVED=$$UPDATE^GMPLDAL(GMPIFN,GMPORIG,GMPFLD,GMPLUSER,GMPROV,ERT)
 ; Save Changes to Notes
 F I=0:0 S I=$O(GMPORIG(10,I)) Q:I'>0  D UPDNOTE^GMPLAPI3(GMPIFN,GMPORIG(10,I),GMPFLD(10,I),GMPROV,ERT)
 I $D(GMPFLD(10,"NEW"))>9 D 
 . M NOTES=GMPFLD(10,"NEW")
 . D NEWNOTE^GMPLAPI3(GMPIFN,GMPROV,GMPVAMC,.NOTES,ERT)
 ; Quit Saving Changes
 D:GMPSAVED DTMOD^GMPLDAL(GMPIFN)
 Q
 ;
DELETE(GMPIFN,GMPROV,GMPVAMC,REASON,ERT) ; DELETE A PROBLEM
 ; Input   GMPIFN   IEN of PROBLEM file
 ;         GMPROV   pointer to NEW PERSON file
 ;         GMPVAMC  pointer to LOCATION file
 ;         REASON   comment
 ;         ERT      Error array root
 S ERT=$G(ERT)
 I $G(GMPIFN)="" D ERR^GMPLAPIE(ERT,"INVALIDPARAM","GMPIFN") Q 0 ; Invalid parameter value
 I $$COND(GMPIFN)="H" D ERR^GMPLAPIE(ERT,"PRBDELETED") Q 0 ; Already deleted
 I $G(GMPVAMC)="" D ERR^GMPLAPIE(ERT,"INVALIDPARAM","GMPVAMC") Q 0 ; Invalid parameter value
 I $G(REASON)'="" D NEWNOTE^GMPLAPI3(GMPIFN,GMPROV,GMPVAMC,REASON,ERT)
 Q $$DELETE^GMPLDAL(GMPIFN,GMPROV,ERT)
 ;
DETAIL(GMPIFN,GMPL) ; Returns Detailed Data for Problem
 ;                
 ; Input   GMPIFN  Pointer to Problem file #9000011
 ;                
 ; Output  GMPL Array, passed by reference
 ;         GMPL("DATA NAME") = External Format of Value
 ;
 ;         GMPL("DIAGNOSIS")  ICD Code
 ;         GMPL("PATIENT")    Patient Name
 ;         GMPL("MODIFIED")   Date Last Modified
 ;         GMPL("NARRATIVE")  Provider Narrative 
 ;         GMPL("ENTERED")    Date Entered ^ Entered by
 ;         GMPL("STATUS")     Status
 ;         GMPL("PRIORITY")   Priority Acute/Chronic
 ;         GMPL("ONSET")      Date of Onset
 ;         GMPL("PROVIDER")   Responsible Provider
 ;         GMPL("RECORDED")   Date Recorded ^ Recorded by
 ;         GMPL("CLINIC")     Hospital Location
 ;         GMPL("SC")         Service Connected SC/NSC/""
 ;
 ;         GMPL("EXPOSURE") = #
 ;         GMPL("EXPOSURE",X)="AGENT ORANGE"
 ;         GMPL("EXPOSURE",X)="RADIATION"
 ;         GMPL("EXPOSURE",X)="ENV CONTAMINANTS"
 ;         GMPL("EXPOSURE",X)="HEAD AND/OR NECK CANCER"
 ;         GMPL("EXPOSURE",X)="MILITARY SEXUAL TRAUMA"
 ;         GMPL("EXPOSURE",X)="COMBAT VET"
 ;         GMPL("EXPOSURE",X)="SHAD"
 ;
 ;         GMPL("COMMENT") = #
 ;         GMPL("COMMENT",CNT) = Date ^ Author ^ Text of Note
 ;              
 N GMPFLD,GMPLP,X,I,FAC,CNT,NIFN
 Q:'$$DETAIL^GMPLDAL(GMPIFN,.GMPFLD)
 S GMPLP=+($$PTR^GMPLUTL4)
 S GMPL("DIAGNOSIS")=$$ICD9NAME^GMPLEXT(GMPFLD(.01))
 S GMPL("PATIENT")=$$PATNAME^GMPLEXT(GMPFLD(.02))
 S GMPL("MODIFIED")=$$EXTDT^GMPLX(GMPFLD(.03))
 S GMPL("NARRATIVE")=$$PROBTEXT^GMPLX(GMPIFN)
 S GMPL("ENTERED")=$$EXTDT^GMPLX(GMPFLD(.08))_U_$$PROVNAME^GMPLEXT(GMPFLD(1.03))
 S GMPL("STATUS")=$S(GMPFLD(.12)="A":"ACTIVE",1:"INACTIVE")
 S X=$S(GMPFLD(.12)'="A":"",1:GMPFLD(1.14))
 S GMPL("PRIORITY")=$S(X="A":"ACUTE",X="C":"CHRONIC",1:"")
 S GMPL("ONSET")=$$EXTDT^GMPLX(GMPFLD(.13))
 S GMPL("PROVIDER")=$$PROVNAME^GMPLEXT(GMPFLD(1.05))
 S GMPL("RECORDED")=$$EXTDT^GMPLX(GMPFLD(1.09))_U_$$PROVNAME^GMPLEXT(GMPFLD(1.04))
 S GMPL("CLINIC")=$$CLINNAME^GMPLEXT(GMPFLD(1.08))
 S GMPL("SC")=$S(GMPFLD(1.10):"YES",GMPFLD(1.10)=0:"NO",1:"UNKNOWN")
 S GMPL("EXPOSURE")=0
 I GMPFLD(1.11) S X=GMPL("EXPOSURE")+1,GMPL("EXPOSURE",X)="AGENT ORANGE",GMPL("EXPOSURE")=X
 I GMPFLD(1.12) S X=GMPL("EXPOSURE")+1,GMPL("EXPOSURE",X)="RADIATION",GMPL("EXPOSURE")=X
 I GMPFLD(1.13) S X=GMPL("EXPOSURE")+1,GMPL("EXPOSURE",X)="ENV CONTAMINANTS",GMPL("EXPOSURE")=X
 I GMPFLD(1.14) S X=GMPL("EXPOSURE")+1,GMPL("EXPOSURE",X)="HEAD AND/OR NECK CANCER",GMPL("EXPOSURE")=X
 I GMPFLD(1.16) S X=GMPL("EXPOSURE")+1,GMPL("EXPOSURE",X)="MILITARY SEXUAL TRAUMA",GMPL("EXPOSURE")=X
 I GMPFLD(1.17) S X=GMPL("EXPOSURE")+1,GMPL("EXPOSURE",X)="COMBAT VET",GMPL("EXPOSURE")=X
 I GMPFLD(1.18)&(GMPLP'>0) S X=GMPL("EXPOSURE")+1,GMPL("EXPOSURE",X)="SHAD",GMPL("EXPOSURE")=X
 S (FAC,CNT)=0,GMPL("COMMENT")=0
 F FAC=0:0 S FAC=$O(^AUPNPROB(GMPIFN,11,FAC)) Q:+FAC'>0  D
 . F NIFN=0:0 S NIFN=$O(^AUPNPROB(GMPIFN,11,FAC,11,NIFN)) Q:NIFN'>0  D
 . . S X=$G(^AUPNPROB(GMPIFN,11,FAC,11,NIFN,0))
 . . S CNT=CNT+1
 . . S GMPL("COMMENT",CNT)=$$EXTDT^GMPLX($P(X,U,5))_U_$$PROVNAME^GMPLEXT($P(X,U,6))_U_$P(X,U,3)
 S GMPL("COMMENT")=CNT D AUDIT^GMPLUTL2
 Q
 ;
VERIFY(GMPIFN,ERT) ; Verify a transcribed problem
 ;
 ; Input   GMPIFN  Pointer to Problem file #9000011
 ;         ERT
 ;
 ; Output 
 S ERT=$G(ERT)
 I $$VERIFIED^GMPLDAL(GMPIFN) D ERR^GMPLAPIE(ERT,"PRBVERIFIED") Q 0 ; BAIL OUT - ALREADY VERIFIED
 Q:'$$LOCK^GMPLDAL(GMPIFN,0,ERT) 0
 D MKPERM^GMPLDAL(GMPIFN)
 D UNLOCK^GMPLDAL(GMPIFN,0)
 Q 1
 ;
DUPL(DFN,TERM,TEXT) ; Check's for Duplicate Entries
 N DA,IFN,NODE0,NODE1
 S DA=0,TEXT=$$UP^XLFSTR(TEXT)
 I '$D(^AUPNPROB("C",TERM))!('$D(^AUPNPROB("AC",DFN))) Q DA
 F IFN=0:0 S IFN=$O(^AUPNPROB("AC",DFN,IFN)) Q:IFN'>0  D  Q:DA>0
 . S NODE0=$G(^AUPNPROB(IFN,0)),NODE1=$G(^(1)) Q:$P(NODE1,U,2)="H"
 . I TERM>1 S:+NODE1=TERM DA=IFN Q
 . S:TEXT=$$UP^XLFSTR($P(^AUTNPOV($P(NODE0,U,5),0),U)) DA=IFN
 Q DA
 ;
CODESTS(PROB,ADATE) ;check status of code associated with a problem
 ; Input:
 ;    PROB  = pointer to the PROBLEM (#9000011) file
 ;    ADATE = FM date on which to check the status of ICD9 code  (opt.) 
 ;
 ; Output:
 ;   1  = ACTIVE on the date passed or current date if not passed
 ;   0  = INACTIVE on the date passed or current date if not passed
 ;
 I '$G(ADATE) S ADATE=DT
 I '$D(^AUPNPROB(PROB,0)) Q 0
 S PROB=$P(^AUPNPROB(PROB,0),U)
 Q +($$STATCHK^ICDAPIU($$CODEC^ICDCODE(+(PROB)),ADATE))
 ;
COND(GMPIFN) ; Returns the condition flag (T,P,H)
 Q $P($G(^AUPNPROB(GMPIFN,1)),U,2)
 ;
STATUS(GMPIFN) ; Returns the status flag (A,I)
 Q $P(^AUPNPROB(GMPIFN,0),U,12)
 ;
ONSET(GMPIFN) ; Returns the date of onset
 Q $P($G(^AUPNPROB(GMPIFN,0)),U,13)
 ;
INACTV(GMPIFN,GMPROV,GMPVAMC,NOTE,RESOLVED,ERT) ; Inactivate problem
 N DA,DR,DIE
 S ERT=$G(ERT)
 I $$STATUS(GMPIFN)="I" D ERR^GMPLAPIE(ERT,"PRBINACTIVE"," ("_$$PROBTEXT^GMPLX(GMPIFN)_")") Q 0
 I $$COND(GMPIFN)="H" D ERR^GMPLAPIE(ERT,"PRBDELETED"," ("_$$PROBTEXT^GMPLX(GMPIFN)_")") Q 0
 D:$G(NOTE)'="" NEWNOTE^GMPLAPI3(GMPIFN,GMPROV,GMPVAMC,NOTE,ERT)
 S DIE="^AUPNPROB(",DR=".12///I;1.07////"_$P($G(RESOLVED),U)
 S DA=GMPIFN D ^DIE
 S CHNGE=GMPIFN_"^.12^"_$$HTFM^XLFDT($H)_U_DUZ_"^A^I^^"_+$G(GMPROV)
 D AUDIT^GMPLX(CHNGE,"")
 D DTMOD^GMPLX(GMPIFN)
 Q 1
 ;
