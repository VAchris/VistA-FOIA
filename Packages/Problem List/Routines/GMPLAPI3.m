GMPLAPI3 ; / Problem List API - NEW,UPDATE NOTES ;02/29/12  12:00
 ;;TBD;Problem List;;02/29/2012
NEWNOTE(GMPIFN,GMPROV,GMPVAMC,NOTES,ERT) ; Creates New Note Entries for Problem
 ; GMPIFN  Pointer to Problem
 ; GMPROV  Current Provider
 ; GMPVAMC Facility
 ; NOTES   Array of notes
 ; ERT     Errors array root
 N HDR,LAST,TOTAL,I,FAC,NIFN
 S ERT=$G(ERT)
 I '$D(NOTES) D ERR^GMPLAPIE(ERT,"INVALIDPARAM","NOTES") Q 0
 I $G(GMPIFN)="" D ERR^GMPLAPIE(ERT,"INVALIDPARAM","GMPIFN") Q 0
 I $G(GMPVAMC)="" D ERR^GMPLAPIE(ERT,"INVALIDPARAM","GMPVAMC") Q 0
 I $$COND^GMPLAPI2(GMPIFN)="H" D ERR^GMPLAPIE(ERT,"PRBDELETED"," ("_$$PROBTEXT^GMPLX(GMPIFN)_")") Q 0
 ; Code Set Versioning (CSV)
 I '$$CODESTS^GMPLAPI2(GMPIFN,DT) D ERR^GMPLAPIE(ERT,"ICDINACT"," ("_$$PROBTEXT^GMPLX(GMPIFN)_")") Q 0
 I $D(NOTES)=1 S NOTES(1)=NOTES
 I '$$LOCK^GMPLDAL(GMPIFN,11,ERT) Q 0
 S FAC=+$O(^AUPNPROB(GMPIFN,11,"B",GMPVAMC,0))
 I 'FAC D
 . S:'$D(^AUPNPROB(GMPIFN,11,0)) ^(0)="^9000011.11PA^^"
 . S HDR=^AUPNPROB(GMPIFN,11,0)
 . S LAST=$P(HDR,"^",3)
 . S TOTAL=$P(HDR,"^",4)
 . F I=(LAST+1):1 Q:'$D(^AUPNPROB(GMPIFN,11,I,0))
 . S ^AUPNPROB(GMPIFN,11,I,0)=GMPVAMC
 . S ^AUPNPROB(GMPIFN,11,"B",GMPVAMC,I)=""
 . S FAC=I
 . S $P(^AUPNPROB(GMPIFN,11,0),U,3,4)=FAC_"^"_(TOTAL+1)
 I FAC>0 D
 . S:'$D(^AUPNPROB(GMPIFN,11,FAC,11,0)) ^(0)="^9000011.1111IA^^"
 . S HDR=^AUPNPROB(GMPIFN,11,FAC,11,0)
 . S LAST=$P(HDR,U,3)
 . S TOTAL=$P(HDR,U,4)
 . F I=(LAST+1):1 Q:'$D(^AUPNPROB(GMPIFN,11,FAC,11,I,0))
 . S NIFN=I
 . F I=0:0 S I=$O(NOTES(I)) Q:I'>0  D
 . . S ^AUPNPROB(GMPIFN,11,FAC,11,NIFN,0)=NIFN_"^^"_NOTES(I)_"^A^"_DT_U_+$G(GMPROV)
 . . S ^AUPNPROB(GMPIFN,11,FAC,11,"B",NIFN,NIFN)=""
 . . S TOTAL=TOTAL+1,LAST=NIFN,NIFN=NIFN+1
 . S $P(^AUPNPROB(GMPIFN,11,FAC,11,0),U,3,4)=LAST_U_TOTAL
 D UNLOCK^GMPLDAL(GMPIFN,11)
 Q 1
 ;
UPDNOTE(GMPIFN,OLDNOTE,NEWNOTE,GMPROV,ERT) ;
 ; GMPIFN
 ; OLDNOTE
 ; NEWNOTE = Note IFN ^ Facility ^ Text
 ; GMPROV
 ; ERT
 Q:NEWNOTE=OLDNOTE
 N NIFN,FAC,TEXT,OLDTEXT
 S NIFN=+NEWNOTE
 S FAC=$P(NEWNOTE,U,2)
 S TEXT=$P(NEWNOTE,U,3)
 S OLDTEXT=$P(OLDNOTE,U,3)
 D:TEXT'="" UPDNOTE^GMPLDAL(GMPIFN,FAC,NIFN,TEXT,OLDTEXT,GMPROV,ERT)
 Q:TEXT=OLDTEXT
 D:TEXT="" DELNOTE^GMPLDAL(GMPIFN,FAC,NIFN,GMPROV,ERT)
 Q
 ;
DELNOTE ;
 Q
 ;
