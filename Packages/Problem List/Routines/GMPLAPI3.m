GMPLAPI3 ; / Problem List API - NEW,UPDATE NOTES ;02/29/12  12:00
 ;;TBD;Problem List;;02/29/2012
NEWNOTE(RETURN,GMPIFN,GMPROV,GMPVAMC,NOTES) ; Creates New Note Entries for Problem
 ; GMPIFN  Pointer to Problem
 ; GMPROV  Current Provider
 ; GMPVAMC Facility
 ; NOTES   Array of notes
 N HDR,LAST,TOTAL,I,FAC,NIFN,DELETED,ICDACTV
 S RETURN=0
 I '$D(NOTES) D ERRX^GMPLAPIE(.RETURN,"INVPARAM","NOTES") Q 0
 I $G(GMPIFN)="" D ERRX^GMPLAPIE(.RETURN,"INVPARAM","GMPIFN") Q 0
 I $G(GMPVAMC)="" D ERRX^GMPLAPIE(.RETURN,"INVPARAM","GMPVAMC") Q 0
 D DELETED^GMPLAPI2(.DELETED,GMPIFN)
 I DELETED D ERRX^GMPLAPIE(.RETURN,"PRBDLTD"," ("_$$PROBTEXT^GMPLX(GMPIFN)_")") Q 0
 ; Code Set Versioning (CSV)
 D CODESTS^GMPLAPI2(.ICDACTV,GMPIFN,DT)
 I 'ICDACTV D ERRX^GMPLAPIE(.RETURN,"ICDINACT"," ("_$$PROBTEXT^GMPLX(GMPIFN)_")") Q 0
 I $D(NOTES)=1 S NOTES(1)=NOTES
 I '$$LOCK^GMPLDAL(GMPIFN,11,"RETURN") Q 0
 S FAC=+$O(^AUPNPROB(GMPIFN,11,"B",GMPVAMC,0))
 I 'FAC D
 . S:'$D(^AUPNPROB(GMPIFN,11,0)) ^(0)="^9000011.11PA^^"
 . S HDR=^AUPNPROB(GMPIFN,11,0)
 . S LAST=$P(HDR,"^",3)
 . S TOTAL=$P(HDR,"^",4)
 . F I=(LAST+1):1 Q:'$D(^AUPNPROB(GMPIFN,11,I,0))
 . S ^AUPNPROB(GMPIFN,11,I,0)=GMPVAMC
 . S ^AUPNPROB(GMPIFN,11,"B",GMPVAMC,I)=""
 . S FAC=I
 . S $P(^AUPNPROB(GMPIFN,11,0),U,3,4)=FAC_"^"_(TOTAL+1)
 I FAC>0 D
 . S:'$D(^AUPNPROB(GMPIFN,11,FAC,11,0)) ^(0)="^9000011.1111IA^^"
 . S HDR=^AUPNPROB(GMPIFN,11,FAC,11,0)
 . S LAST=$P(HDR,U,3)
 . S TOTAL=$P(HDR,U,4)
 . F I=(LAST+1):1 Q:'$D(^AUPNPROB(GMPIFN,11,FAC,11,I,0))
 . S NIFN=I
 . F I=0:0 S I=$O(NOTES(I)) Q:I'>0  D
 . . S ^AUPNPROB(GMPIFN,11,FAC,11,NIFN,0)=NIFN_"^^"_NOTES(I)_"^A^"_DT_U_+$G(GMPROV)
 . . S ^AUPNPROB(GMPIFN,11,FAC,11,"B",NIFN,NIFN)=""
 . . S TOTAL=TOTAL+1,LAST=NIFN,NIFN=NIFN+1
 . S $P(^AUPNPROB(GMPIFN,11,FAC,11,0),U,3,4)=LAST_U_TOTAL
 D UNLOCK^GMPLDAL(GMPIFN,11)
 S RETURN=1
 Q RETURN
 ;
UPDNOTE(RETURN,GMPIFN,OLDNOTE,NEWNOTE,GMPROV) ;
 ; GMPIFN
 ; OLDNOTE
 ; NEWNOTE = Note IFN ^ Facility ^ Text
 ; GMPROV
 ; ERT
 Q:NEWNOTE=OLDNOTE
 N NIFN,FAC,TEXT,OLDTEXT
 S NIFN=+NEWNOTE
 S FAC=$P(NEWNOTE,U,2)
 S TEXT=$P(NEWNOTE,U,3)
 S OLDTEXT=$P(OLDNOTE,U,3)
 D:TEXT'="" UPDNOTE^GMPLDAL(GMPIFN,FAC,NIFN,TEXT,OLDTEXT,GMPROV,"RETURN")
 Q:TEXT=OLDTEXT
 D:TEXT="" DELNOTE^GMPLDAL(GMPIFN,FAC,NIFN,GMPROV,"RETURN")
 S RETURN=1
 Q RETURN
 ;
DELNOTE ;
 Q
 ;
