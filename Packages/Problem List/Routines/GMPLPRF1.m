GMPLPRF1 ; SLC/MKB -- Problem List User Prefs cont ;09/20/12
 ;;2.0;Problem List;**3,260002**;Aug 25, 1994
ADD ; -- add item(s) to view
 N GMPLSEL,GMPLNO,IFN,NUM,CNUM,I,J,MSG,PARAM S VALMBCK=$S(VALMCC:"",1:"R")
ADD1 S GMPLSEL=$$SELECT Q:GMPLSEL="^"
 S GMPLNO=$L(GMPLSEL,","),I=$S(GMPLMODE="S":1250000.382,1:1250000.383)
 I (^TMP("GMPLIST",$J,"VIEW",0)+GMPLNO-1)>60 D  G ADD1
 . K MSG
 . S PARAM(1)=^TMP("GMPLIST",$J,"VIEW",0),PARAM(2)=60-^TMP("GMPLIST",$J,"VIEW",0)
 . D BLD^DIALOG(I,.PARAM,,"MSG")
 . D EN^DDIOL(.MSG)
ADD2 ;
 S I=$S(GMPLMODE="S":1250000.387,1:1250000.388)
 D BLD^DIALOG(I,GMPLNO-1,,"MSG")
 D EN^DDIOL($$EZBLD^DIALOG(I,GMPLNO-1),,"!!")
 K I
 N CHLD
 F I=1:1:GMPLNO S NUM=$P(GMPLSEL,",",I) I NUM D
 . S IFN=^TMP("GMPLIST",$J,"IDX",NUM) W "."
 . S TMP=$E($P(^TMP("GMPLIST",$J,NUM,0),U,1),1,44)_" Y",^TMP("GMPLIST",$J,NUM,0)=TMP
 . S:'$D(^TMP("GMPLIST",$J,"VIEW",IFN)) ^TMP("GMPLIST",$J,"VIEW",IFN)="",^TMP("GMPLIST",$J,"VIEW",0)=^TMP("GMPLIST",$J,"VIEW",0)+1
 . Q:GMPLMODE'="S"
 . D SVCCHLD^GMPLEXT(IFN,.CHLD)
 . Q:CHLD=0
 . I $$INCLCHLD(IFN) F J=0:0 S J=$O(CHLD(J)) Q:J'>0  D
 . . S CNUM=+$G(^TMP("GMPLIST",$J,"B",J)) W "."
 . . I CNUM S TMP=$E(^TMP("GMPLIST",$J,CNUM,0),1,44)_" Y",^TMP("GMPLIST",$J,CNUM,0)=TMP S:'$D(^TMP("GMPLIST",$J,"VIEW",J)) ^TMP("GMPLIST",$J,"VIEW",J)="",^TMP("GMPLIST",$J,"VIEW",0)=^TMP("GMPLIST",$J,"VIEW",0)+1
 S VALMBCK="R",VALMSG=$$MSG^GMPLPREF K VALMHDR
 Q
 ;
REMOVE ; -- delete item(s) from view
 N GMPLSEL,GMPLNO,IFN,NUM,CNUM,I,J,CHLD,MSG S VALMBCK=$S(VALMCC:"",1:"R")
 S GMPLSEL=$$SELECT Q:GMPLSEL="^"
 S GMPLNO=$L(GMPLSEL,",")
 S MSG=$S(GMPLMODE="S":1250000.389,1:1250000.390)
 D EN^DDIOL($$EZBLD^DIALOG(MSG,GMPLNO-1),,"!!")
 F I=1:1:GMPLNO S NUM=$P(GMPLSEL,",",I) I NUM D
 . S IFN=+^TMP("GMPLIST",$J,"IDX",NUM),^TMP("GMPLIST",$J,NUM,0)=$E(^TMP("GMPLIST",$J,NUM,0),1,44)
 . S:$D(^TMP("GMPLIST",$J,"VIEW",IFN)) ^TMP("GMPLIST",$J,"VIEW",0)=^TMP("GMPLIST",$J,"VIEW",0)-1
 . K ^TMP("GMPLIST",$J,"VIEW",IFN) W "."
 . Q:GMPLMODE'="S"
 . D SVCCHLD^GMPLEXT(IFN,.CHLD)
 . Q:CHLD=0
 . I $$INCLCHLD(IFN) F J=0:0 S J=$O(CHLD(J)) Q:J'>0  D
 . . S CNUM=+$G(^TMP("GMPLIST",$J,"B",J)) W "."
 . . I CNUM S ^TMP("GMPLIST",$J,CNUM,0)=$E(^TMP("GMPLIST",$J,CNUM,0),1,44) S:$D(^TMP("GMPLIST",$J,"VIEW",J)) ^TMP("GMPLIST",$J,"VIEW",0)=^TMP("GMPLIST",$J,"VIEW",0)-1 K ^TMP("GMPLIST",$J,"VIEW",J)
 S VALMBCK="R",VALMSG=$$MSG^GMPLPREF K VALMHDR
 Q
 ;
INCLCHLD(IFN) ; Returns 1 or 0, to include 'child' services in selection
 N DIR,X,Y,NAME S NAME=$$SVCNAME^GMPLEXT(IFN),DIR("B")="YES"
 S DIR(0)="Y"
 D BLD^DIALOG(1250000.391,NAME,,"DIR(""A"")")
 D BLD^DIALOG(1250000.392,,,"DIR(""?"")")
 D ^DIR S:$D(DTOUT) Y="^"
 Q Y
 ;
SELECT() ; Select item(s) from list
 N DIR,X,Y,MAX,MSG
 S MAX=+$G(^TMP("GMPLIST",$J,0)) I MAX'>0 Q "^"
 S DIR(0)="LAO^1:"_MAX
 S MSG=$S(GMPLMODE="S":1250000.393,1:1250000.394)
 D BLD^DIALOG(MSG,,,"DIR(""A"")")
 S:MAX>1 DIR("A")=DIR("A")_" (1-"_MAX_"): "
 S:MAX'>1 DIR("A")=DIR("A")_": ",DIR("B")=1
 S MSG=$S(GMPLMODE="S":1250000.395,1:1250000.396)
 D BLD^DIALOG(MSG,,,"DIR(""?"")")
 D ^DIR I $D(DTOUT)!(X="") S Y="^"
 Q Y
 ;
DIFFRENT() ; -- Returns 1 if view has changed, else 0
 N I,TEMP S TEMP=""
 F I=0:0 S I=$O(^TMP("GMPLIST",$J,"VIEW",I)) Q:I'>0  S TEMP=TEMP_I_"/"
 S:$L(TEMP) TEMP=GMPLMODE_"/"_TEMP
 Q TEMP'=$$USERVIEW^GMPLEXT()
 ;
SAVE ; -- save new view in File #200/Field #125
 N I,TEMP,TMP S TEMP=GMPLMODE,TMP=$$USERLST^GMPLEXT()
 F I=0:0 S I=$O(^TMP("GMPLIST",$J,"VIEW",I)) Q:I'>0  S TEMP=TEMP_"/"_I
 S:$L(TEMP)>1 TEMP=TEMP_"/"
 D UPUSRLST^GMPLEXT(TEMP,TMP)
 S GMPSAVED=1
 Q
 ;
SWITCH ; -- change preferred views (service <--> clinic)
 N DIR,X,Y,MSG S Y=1,VALMBCK=$S(VALMCC:"",1:"R")
 G SW1:'$L($G(GMPLVIEW)) ; no current view
 S DIR(0)="Y",DIR("B")="NO"
 D BLD^DIALOG(1250000.397,,,"DIR(""A"")")
 D BLD^DIALOG(1250000.398,,,"DIR(""?"")")
 D BLD^DIALOG($S(GMPLMODE="S":1250000.399,1:1250000.400),,,"MSG")
 D EN^DDIOL(.MSG)
 D ^DIR
SW1 I Y D
 . S VALMBCK="R",GMPLVIEW="",GMPLMODE=$S(GMPLMODE="S":"C",1:"S")
 . D @("GET"_GMPLMODE_"LIST^GMPLPREF") K VALMHDR
 Q
 ;
DELETE ; -- delete preferred view (no view)
 N DIR,X,Y,MSG S DIR(0)="Y",DIR("B")="NO",VALMBCK=$S(VALMCC:"",1:"R")
 D BLD^DIALOG(1250000.401,,,"DIR(""A"")")
 D BLD^DIALOG(1250000.402,,,"DIR(""?"")")
 D ^DIR I Y D
 . S MSG=$S(GMPLMODE="S":1250000.403,1:1250000.404)
 . D EN^DDIOL(MSG,,"!!")
 . K ^TMP("GMPLIST",$J,"VIEW") S ^TMP("GMPLIST",$J,"VIEW",0)=0,VALMBCK="Q",GMPLMODE=""
 . D SAVE D EN^DDIOL($$EZBLD^DIALOG(1250000.095),,"?0")
 Q
