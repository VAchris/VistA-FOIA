GMPLAPI1 ;; Build Problem Selection Lists ; 02/21/12 10:23
 ;;;Problem List;;02/21/12
NEWLST(GMPLLST,GMPLLOC,ERRROOT) ; Add new Problem Selection List
 ; Input
 ;  GMPLLST  Problem Selection List name
 ;  GMPLLOC  Location IEN
 ; Output:  IEN of Problem Selection List  
 I $D(GMPLLST)'>0!$L(GMPLLST)=0 D ERR^GMPLAPIE(.ERRROOT,"INVALIDPARAM","GMPLLST") Q 0
 I $L($G(GMPLLOC))>0 D:+$G(GMPLLOC)'>0 ERR^GMPLAPIE(.ERRROOT,"INVALIDPARAM","GMPLLOC") Q:+$G(GMPLLOC)'>0 0
 N EXISTS,NEWLST
 S EXISTS=$$FIND("125",GMPLLST)
 I EXISTS>0 D ERR^GMPLAPIE(.ERRROOT,"LISTEXIST") Q EXISTS
 I $G(GMPLLOC) D  I '$$FIND(44,GMPLLOC) D ERR^GMPLAPIE(.ERRROOT,"LOCNOTFOUND") Q 0
 S NEWLST=$$CREATE(GMPLLST,"^GMPL(125,","125")
 I +NEWLST'>0 D ERR^GMPLAPIE(.ERRROOT,"INVALIDPARAM","GMPLLST") Q 0
 I $G(GMPLLOC) D ADDLOC(NEWLST,GMPLLOC,.ERRROOT)
 Q NEWLST
 ;
ASSGNUSR(GMPLSLST,GMPLUSER,ERRROOT) ; Assign Problem Selection List to users
 ; Input
 ;  GMPLLST   List IEN
 ;  GMPLUSER  List of users (^UserIEN^...)
 I '$G(GMPLSLST) D ERR^GMPLAPIE(.ERRROOT,"INVALIDPARAM","GMPLSLST") Q 0
 N EXISTS,DIE,DR,DA,GMPLI,U,UE
 S DIE="^VA(200,",U="^",UE=0,EXISTS=$$FIND("125",GMPLSLST)
 I EXISTS'>0 D ERR^GMPLAPIE(.ERRROOT,"LISTNOTFOUND") Q
 I '$$VALLIST^GMPLBLD2(+GMPLSLST) D ERR^GMPLAPIE(.ERRROOT,"INACTIVEICD9") Q
 F GMPLI=1:1:$L(GMPLUSER,U) S DA=$P(GMPLUSER,U,GMPLI) I DA D
 . I '$G(DA) D ERR^GMPLAPIE(.ERRROOT,"INVALIDPARAM","GMPLUSER") S UE=1 Q
 . I '$$FIND("200",DA) D ERR^GMPLAPIE(.ERRROOT,"PROVNOTFOUND") S UE=1
 Q:UE>0
 S DR="125.1////"_+GMPLSLST
 F GMPLI=1:1:$L(GMPLUSER,U) S DA=$P(GMPLUSER,U,GMPLI) I DA D
 . D ^DIE
 Q
 ;
REMUSR(GMPLSLST,GMPLUSER,ERRROOT) ; Remove Problem Selection List from users
 ; Input
 ;  GMPLLST   List IEN
 ;  GMPLUSER  List of users (^UserIEN^...)
 I '$G(GMPLSLST) D ERR^GMPLAPIE(.ERRROOT,"INVALIDPARAM","GMPLSLST") Q 0
 N EXISTS,DIE,DR,DA,GMPLI,U,UE
 S DIE="^VA(200,",U="^",UE=0,EXISTS=$$FIND("125",GMPLSLST)
 I EXISTS'>0 D ERR^GMPLAPIE(.ERRROOT,"LISTNOTFOUND") Q
 I '$$VALLIST^GMPLBLD2(+GMPLSLST) D ERR^GMPLAPIE(.ERRROOT,"INACTIVEICD9") Q
 F GMPLI=1:1:$L(GMPLUSER,U) S DA=$P(GMPLUSER,U,GMPLI) I DA D
 . I '$G(DA) D ERR^GMPLAPIE(.ERRROOT,"INVALIDPARAM","GMPLUSER") S UE=1 Q
 . I '$$FIND("200",DA) D ERR^GMPLAPIE(.ERRROOT,"PROVNOTFOUND") S UE=1
 Q:UE>0
 S DR="125.1///@"
 F GMPLI=1:1:$L(GMPLUSER,U) S DA=$P(GMPLUSER,U,GMPLI) I DA D
 . D ^DIE
 Q
 ;
DELSLST(GMPLSLST,ERRROOT) ; Delete Problem Selection List
 ; Input
 ;  GMPLSLST  Problem Selection List IEN
 I '$G(GMPLSLST) D ERR^GMPLAPIE(.ERRROOT,"INVALIDPARAM","GMPLSLST") Q
 N U,USER,VIEW,GMPCOUNT,DIK,DA
 S GMPCOUNT=0,U="^",EXISTS=$$FIND("125",GMPLSLST)
 I EXISTS'>0 D ERR^GMPLAPIE(.ERRROOT,"LISTNOTFOUND") Q
 F USER=0:0 S USER=$O(^VA(200,USER)) Q:USER'>0  D
 . S VIEW=$P($G(^VA(200,USER,125)),U,2) Q:'VIEW  Q:VIEW'=+GMPLSLST
 . S GMPCOUNT=GMPCOUNT+1
 I GMPCOUNT D ERR^GMPLAPIE(.ERRROOT,"LISTUSED",GMPCOUNT_" user(s) are currently assigned this list!") Q
 Q:'$$LOCK("125",GMPLSLST,.ERRROOT)
 S DIK="^GMPL(125.1,",DA=0 
 F  S DA=$O(^GMPL(125.1,"B",+GMPLSLST,DA)) Q:DA'>0  D ^DIK
 S DA=+GMPLSLST,DIK="^GMPL(125," D ^DIK
 D UNLOCK("125",GMPLSLST)
 Q
 ;
ADDCATEG(GMPLCAT,ERRROOT) ; Add new Category
 ; Input
 ;  GMPLCAT  Category name
 I $D(GMPLCAT)'>0!$L(GMPLCAT)=0 D ERR^GMPLAPIE(.ERRROOT,"INVALIDPARAM","GMPLCAT") Q 0
 N EXISTS,NEWCAT
 S NEWCAT="",EXISTS=$$FIND("125.11",GMPLCAT)
 I EXISTS>0 D ERR^GMPLAPIE(.ERRROOT,"CATEGEXIST") Q NEWCAT
 S NEWCAT=$$CREATE(GMPLCAT,"^GMPL(125.11,","125.11")
 I +NEWCAT'>0 D ERR^GMPLAPIE(.ERRROOT,"INVALIDPARAM","GMPLCAT") Q 0
 Q NEWCAT
 ;
FIND(FILENO,EL) ; Lookup
 ; Input
 ;  EL      IEN or name of the record
 ;  FILENO  File number
 ; Output:
 ;  0   - if no exact matches are found
 ;  IEN - if a single match is found
 N PRE S PRE="" 
 I +EL>0 S EL=+EL,PRE="`"
 Q $$FIND1^DIC(FILENO,"","MX",PRE_EL,"","","ERR")
 ;
CREATE(GMPLNAME,GMPLFILE,GMPLFN) ; Create simple record
 ; Input
 ;  GMPLNAME  Name of the new record
 ;  GMPLFILE  File name
 ;  GMPLFN    File number
 ; Output
 ;  IEN  New record IEN
 ;  -1   On error
 N DIC,X,Y,DLAYGO
 S DIC=GMPLFILE,DIC(0)="OMZLX",DLAYGO=GMPLFN,X=GMPLNAME
 D ^DIC S:Y>0 Y=+Y_U_Y(0) Q Y
 ;
ADDLOC(GMPLLST,GMPLLOC,ERRROOT) ; Add location to list
 N DIE,DA,DR
 Q:'$$LOCK("125",GMPLLST,.ERRROOT) 0
 S DIE="^GMPL(125,",DA=+GMPLLST,DR=".03////"_+GMPLLOC D ^DIE
 D UNLOCK("125",GMPLLST)
 Q 1
 ;
LOCK(GMPLFN,GMPLIEN,ERRROOT)
 L +^GMPL(GMPLFN,+GMPLIEN,0):1
 I '$T D ERR^GMPLAPIE(.ERRROOT,"FILELOCKED") Q 0
 Q 1
 ;
UNLOCK(GMPLFN,GMPLIEN)
 L -^GMPL(GMPLFN,+GMPLIEN,0)
 Q
 ;