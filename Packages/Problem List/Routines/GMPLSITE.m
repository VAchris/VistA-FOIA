GMPLSITE ;RGI -- GMPL Site parameters ; 04/06/12
 ;;2.0;Problem List;**260002**;Aug 25, 1994
 Q  ;
 ;
GET(RETURN) ;
 N X
 S X=$G(^GMPL(125.99,1,0))
 S RETURN("VER")=+$P(X,"^",2)
 S RETURN("PRT")=+$P(X,"^",3)
 S RETURN("CLU")=+$P(X,"^",4)
 S RETURN("REV")=$S($P(X,"^",5)="R":1,1:0)
 S RETURN("SDP")=+$P(X,"^",6)
 Q
 ;
VERIFY()    ;
 N PARAMS
 D GET(.PARAMS)
 Q PARAMS("VER")
 ;
GETPAR(RETURN) ;
 N X
 S X=$G(^GMPL(125.99,1,0))
 S RETURN("VER")=$P(X,"^",2)
 S RETURN("PRT")=$P(X,"^",3)
 S RETURN("CLU")=$P(X,"^",4)
 S RETURN("REV")=$P(X,"^",5)
 S RETURN("SDP")=$P(X,"^",6)
 Q 1
 ;
SET(RETURN,PARAMS) ;
 N FDAROOT,CHOICES,HASERR,HASELEM,I
 S CHOICES=",VER,PRT,CLU,REV,SDP,"
 S HASELEM=0,HASERR=0,I=""
 F  S I=$O(PARAMS(I)) Q:I=""  S HASELEM=1 D
 . I CHOICES'[(","_I_",") S HASERR=1 D ERR^GMPLAPIE("RETURN","INVALIDPARAM",I) Q
 . I PARAMS(I)="" Q
 . I I'="REV",PARAMS(I)'=1,PARAMS(I)'=0,PARAMS(I)'="@" S HASERR=1 D ERR^GMPLAPIE("RETURN","INVALIDPARAM",PARAMS(I)_" @"_I) Q
 . I I="REV",PARAMS(I)'="R",PARAMS(I)'="C",PARAMS(I)'="@" S HASERR=1 D ERR^GMPLAPIE("RETURN","INVALIDPARAM",PARAMS(I)_" @"_I) Q
 I HASERR Q 0
 I 'HASELEM D ERR^GMPLAPIE("RETURN","INVALIDPARAM","NO KEY") Q 0
 S:$D(PARAMS("VER")) FDAROOT(125.99,"1,",1)=PARAMS("VER")
 S:$D(PARAMS("PRT")) FDAROOT(125.99,"1,",2)=PARAMS("PRT")
 S:$D(PARAMS("CLU")) FDAROOT(125.99,"1,",3)=PARAMS("CLU")
 S:$D(PARAMS("REV")) FDAROOT(125.99,"1,",4)=PARAMS("REV")
 S:$D(PARAMS("SDP")) FDAROOT(125.99,"1,",6)=PARAMS("SDP")
 D FILE^DIE("K","FDAROOT")
 Q 1
 ;
