GMTSPLSZ ;SLC/SBW - Updated Problem List 2.0 extract routine. in HS namespace ;18/Apr/95
 ;;2.7;Health Summary;;Oct 20, 1995
GMPLHS ;SLC/MKB,DJP,SBW - Extract for Prob List-Health Summary 2.7 ;15/APR/95
 ;;2.0;Problem List;;Aug 25, 1994
GETLIST(GMPDFN,STATUS) ; Define list 
 N GMPLIST,GMPARAM,GMPCNT
 K ^TMP("GMPLHS",$J)
 Q:+GMPDFN'>0
 S GMPCNT=0
 D GET^GMPLSITE(.GMPARAM) S GMPARAM("QUIET")=1
 D GETPLIST^GMPLAPI4(.GMPLIST,GMPDFN,STATUS,GMPARAM("REV"),"","",0)
BUILD
 N NUM
 I GMPLIST(0)'>0 K ^TMP("GMPLHS",$J) Q
 S ^TMP("GMPLHS",$J,STATUS,0)=GMPLIST(0)_U_GMPLIST ; # entries extracted ^ # entries that exist
 F NUM=1:1:GMPLIST(0) D GETPROB(GMPLIST(NUM),.GMPCNT)
 Q
GETPROB(IFN,GMPCNT) ; Get problem data and set it to ^TMP array
 ; Returns ^TMP("GMPLHS",$J,CNT,0)
 ; Returns ^TMP("GMPLHS",$J,CNT,IFN,"N")
 N GMPL,NOTES,LOC,NUM,NODE
 D DETAIL^GMPLAPI2(.GMPL,IFN)
 S DIAG=$P(GMPL(.01),U)
 S LASTMDT=$P(GMPL(.03),U)
 S NARR=$P(GMPL(.05),U,2)
 S SITE=$P(GMPL(.06),U,2)
 S ENTDT=$P(GMPL(.08),U)
 S STAT=$P(GMPL(.12),U)
 S ONSETDT=$P(GMPL(.13),U)
 S RPROV=$P(GMPL(1.05),U,2)
 S SERV=$P(GMPL(1.06),U,2)
 S SERVABB=$$SERV($P(GMPL(1.06),U),SERV)
 S RESDT=$P(GMPL(1.07),U)
 S CLIN=$P(GMPL(1.08),U,2)
 S RECDT=$P(GMPL(1.09),U)
 S GMPCNT=GMPCNT+1 ; # of problems included in the extract
 S ^TMP("GMPLHS",$J,GMPCNT,0)=DIAG_U_LASTMDT_U_SITE_U_ENTDT_U_STAT_U_ONSETDT_U_RPROV_U_SERV_U_SERVABB_U_RESDT_U_CLIN_U_RECDT
 S ^TMP("GMPLHS",$J,GMPCNT,"N")=NARR
 D FULLNTE^GMPLAPI3(.NOTES,IFN) ; Add active comments
 Q:NOTES'>0
 S LOC=0
 F  S LOC=$O(NOTES(LOC)) Q:LOC=""  D
 . S NUM=0
 . F  S NUM=$O(NOTES(LOC,NUM)) Q:NUM=""  D
 . . S NODE=NOTES(LOC,NUM)
 . . S ^TMP("GMPLHS",$J,GMPCNT,"C",LOC,NUM,0)=$P(NODE,U,3)_U_$P(NODE,U,5)_U_$$PROVNAME($P(NODE,U,6))
 Q
 ;
PROVNAME(GMPROV) ; Returns provider name
 Q $P($G(^VA(200,+GMPROV,0)),U)
 ;
SERV(X,SERV) ; Return service name abbreviation
 N ABBREV
 S ABBREV=$P($G(^DIC(49,+X,0)),U,2)
 I ABBREV="" S ABBREV=$E($G(SERV),1,5)
 Q ABBREV
